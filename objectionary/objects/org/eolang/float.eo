# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.38.4
+version 0.38.4

# The `float` object is an abstraction of a 64-bit floating-point number that
# internally is a chain of eight bytes.
[as-bytes] > float
  as-bytes > @

  # Tests that $ = x.
  [x] > eq
    x > x-as-bytes!
    ^ > self-as-bytes!
    nan > nan-as-bytes!
    0.0 > pos-zero-as-bytes!
    -0.0 > neg-zero-as-bytes!
    if. > @
      or.
        eq.
          x-as-bytes
          nan-as-bytes
        eq.
          self-as-bytes
          nan-as-bytes
      false
      or.
        and.
          or.
            eq.
              x-as-bytes
              pos-zero-as-bytes
            eq.
              x-as-bytes
              neg-zero-as-bytes
          or.
            eq.
              self-as-bytes
              pos-zero-as-bytes
            eq.
              self-as-bytes
              neg-zero-as-bytes
        eq.
          self-as-bytes
          x-as-bytes

  # Tests that $ < x.
  [x] > lt
    0.0.gt > @
      ^.minus
        float
          x > value!

  # Tests that $ ≤ x.
  [x] > lte
    x > value!
    or. > @
      ^.eq value
      ^.lt value

  # Tests that $ > x.
  [x] > gt /bool

  # Tests that $ ≥ x.
  [x] > gte
    x > value!
    or. > @
      ^.eq value
      ^.gt value

  # Multiplication of $ and x.
  [x] > times /float

  # Sum of $ and x.
  [x] > plus /float

  # Negation of $.
  ^.times -1.0 > [] > neg

  # Difference between $ and x.
  ^.plus x.neg > [x] > minus

  # Quotient of the division of $ by x.
  [x] > div /float
