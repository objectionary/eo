# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+package org.eolang
+rt jvm org.eolang:eo-runtime:0.23.5

[] > float
  # Tests that $ = x
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that $ â‰  x
  [x] > neq
    (^.eq x).not > @

  # Tests that $ < x
  [x] > less
    and. > @
      (^.eq x).not
      (^.greater x).not

  # Tests that $ â‰¤ x
  [x] > leq
    or. > @
      ^.eq x
      ^.less x

  # Tests that $ > x
  [x] > greater /bool

  # Tests that $ â‰¥ x
  [x] > geq
    or. > @
      ^.eq x
      ^.greater x

  # Multiplication of $ and x
  [x] > mul /float

  # Sum of $ and x
  [x] > add /float

  # Negation of $
  [] > neg
    ^.mul -1.0 > @

  # Sign of $ (-1.0, 0.0, or 1.0)
  [] > signum
    if. > @
      ^.less 0.0
      -1.0
      if.
        ^.eq 0.0
        0.0
        1.0

  # Difference between $ and x
  [x] > sub
    ^.add (x.neg) > @

  # x'th power of $
  [x] > pow /float

  # Quotient of the division of $ by x
  [x] > div
    ^.mul (x.pow -1.0) > @

  # Absolute value of $ (i.e., with no sign)
  [] > abs
    if. > @
      ^.geq 0.0
      ^
      ^.neg

  # Sine of $
  [] > sin /float

  # Cosine of $
  [] > cos /float

  # Converts this to bytes
  [] > as-bytes /bytes

  # Converts this to int
  [] > as-int /int

  # Converts this to string
  [] > as-string /string

  # Converts this to hash
  [] > as-hash /int
