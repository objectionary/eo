+alias org.eolang.structs.bytes-as-array
+alias org.eolang.structs.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.structs
+version 0.57.3
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Represents sequence of bytes as array.
# Here `bts` is `bytes` objects, the return value is `tuple` where
# each element is one `byte`.
[bts] > bytes-as-array
  slice-byte * 0 > @
  bts > dataized-bts!
  dataized-bts.size > bytes-size!

  [tup index] > slice-byte
    if. > @
      index.lt bytes-size
      slice-byte
        tup.with (bts.slice index 1)
        (index.plus 1).as-number
      tup

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-converts-bytes-to-array
    eq. > @
      list
        bytes-as-array
          20-1F-EE-B5-FF
      * 20- 1F- EE- B5- FF-

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-single-byte-to-array
    eq. > @
      list
        bytes-as-array
          1F-
      * 1F-

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-zero-bytes-to-array
    eq. > @
      list
        bytes-as-array
          --
      *
