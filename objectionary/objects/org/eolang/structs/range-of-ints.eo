+alias org.eolang.structs.range
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.structs
+version 0.55.2
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Range of integers from `start` to `end` (soft border) with step = 1.
# Here `start` and `end` must be `int`s. If they're not - an error will
# be returned.
[start end] > range-of-ints
  if. > @
    and.
      or.
        0.eq start
        1.eq (start.div start)
      or.
        0.eq end
        1.eq (end.div end)
    range
      [] >>
        [num] > build
          num > @
          ^.build (1.plus @) > next
        build ^.start > @
      end
    error "Some of the arguments are not integers"
