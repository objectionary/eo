+alias org.eolang.structs.list
+alias org.eolang.structs.map
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.structs
+version 0.58.8
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:26
+unlint redundant-object:27

# Set - is an unordered `list` of unique objects.
[lst] > set
  initialized > @
    map
      list
        lst
      .mapped
        map.entry item true > [item]
      .origin

  # Initialized set with rebuilt unique sequence.
  [mp] > initialized
    mp.keys > @
    mp.size > size

    # Append new `item` to set. It must be possible to get
    # hash code of `item` so `item` must be dataizable.
    [item] > with
      set.initialized > @
        mp.with item true

    # Remove `item` from `set`. If `item` is not present,
    # the set won't be changed.
    [item] > without
      set.initialized > @
        mp.without item

    # Check if given `item` exists in set.
    mp.has item > [item] > has

  # Tests that set rebuilds itself removing duplicates.
  [] +> tests-set-rebuilds-itself
    eq. > @
      set
        * 1 2 2
      * 1 2

  # Tests that set does not append an existing item.
  [] +> tests-does-not-append-existed-item-to-set
    set
      * 1 2
    .with 1
    .size
    .eq 2 > @

  # Tests that set appends a new item.
  [] +> tests-appends-new-item-to-set
    set
      * 1 2
    .with 3
    .has 3 > @

  # Tests that empty set has size of zero.
  [] +> tests-empty-set-has-size-of-zero
    set
      *
    .size
    .eq 0 > @
