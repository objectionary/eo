+alias org.eolang.structs.hash-code-of
+alias org.eolang.structs.list
+alias org.eolang.txt.sprintf
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.structs
+version 0.56.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Hash-map.
# Here `pairs` must be a `tuple` of `map.entry` object.
# The `map.entry.key` must be a dataizable object, `map.entry.value` may be any object.
[pairs] > map
  this. > @
    [] >>
      initialized > @
        as-tuple.
          if.
            pairs.length.eq 0
            *
            entries.
              rec-rebuild
                pairs

      [entries hashes] > couple
        $ > this

      [tup] > rec-rebuild
        if. > @
          tup.length.eq 0
          couple * (list *)
          if.
            prev.hashes.contains hash
            prev
            couple
              prev.entries.with
                [] >>
                  tup.value.key > key
                  tup.value.value > value
                  ^.hash > hash
              prev.hashes.with hash
        hash-code-of tup.value.key > hash!
        (rec-rebuild tup.prev).this > prev

  # Hash map entry.
  # Here 'key' is an object which is used to find `value` is hash map.
  # The object `key` must be dataizable.
  # The `value` is an object which is used as value stored in hash map by
  # given `key`.
  [key value] > entry

  # Initialized hash map with rebuilt entries.
  [entries] > initialized
    $ > this
    entries.length > size
    # Returns `list` of all keys in hash map.
    # Keys order is not guaranteed.
    mapped. > keys
      list entries
      entry.key > [entry]
    # Returns `list` of all values in hash map.
    # Values order is not guaranteed.
    mapped. > values
      list entries
      entry.value > [entry]

    # Returns `true` if hash map has object by given `key`.
    # Here `key` must be dataizable.
    (found key).exists > [key] > has

    # Tries to find object in hash map by given `key`.
    # Here `key` must be dataizable.
    # The returned object has two attributes: `exists` and `get`.
    # The `exists` attribute is either `true`, if object was found,
    # or `false` if was not.
    # The `get` attribute returns either found object, or `error` if
    # the object wasn't found.
    # @todo #3251:30min Find a way to link hash code and index of key.
    #  Right now map is implemented as `tuple` of objects where every
    #  element is composition of three entities: hash, key and value.
    #  When we try to find an element in hash map by key (K1) we're
    #  calculating hash of K1 (H1) and trying to find the entity where
    #  `entity.hash` (H2) is equal to H1. This search is implemented by
    #  simple reducing initial hash map `tuple` and obviously slow - O(n).
    #  We need to find a way to get a right index of entity in hash map
    # `tuple` just by applying some simple operation on H1, similar to it's
    #  implemented in other programming languages. Then we'll get O(1) on
    #  `found` operation.
    [key] > found
      if. > @
        size.eq 0
        not-found
        rec-key-search entries
      hash-code-of key > hash!

      [tup] > rec-key-search
        if. > @
          tup.length.eq 0
          not-found
          if.
            prev.exists
            prev
            if.
              tup.value.hash.eq hash
              [] >>
                $ > this
                true > exists
                tup.value.value > get
              not-found
        (rec-key-search tup.prev).this > prev

      [] > not-found
        $ > this
        false > exists
        error > get
          sprintf
            "Object by hash code %d from given key does not exists"
            * hash

    # Returns the new `map` with added object
    # Replaces if there was one before.
    [key value] > with
      this. > @
        map.initialized
          with.
            origin.
              filtered.
                list entries
                (hash.eq entry.hash).not > [entry] >>
            [] >>
              ^.key > key
              ^.value > value
              ^.hash > hash
      hash-code-of key > hash!

    # Returns a new `map`, without element with the given `key`
    # Returns the `map` itself, if there was no item with this `key`.
    [key] > without
      this. > @
        map.initialized
          origin.
            filtered.
              list entries
              (hash.eq entry.hash).not > [entry] >>
      hash-code-of key > hash!
