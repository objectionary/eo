+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.txt
+rt jvm org.eolang:eo-runtime:0.56.9
+rt node eo2js-runtime:0.0.0
+version 0.56.9
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint wrong-sprintf-arguments:58
+unlint wrong-sprintf-arguments:66
+unlint sprintf-without-formatters:72

# The `sprintf` object allows you to format and output text depending
# on the arguments passed to it and return it as `org.eolang.string`.
#
# When arguments are processed - they're dataized and converted to objects
# depending on next flags:
# - %s - converts object to `string`
# - %d - converts object to `int`
# - %f - converts object to `float`
# - %x - converts object to `bytes`
# - %b - converts object to boolean, `true` or `false`.
[format args] > sprintf ?
  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-prints-simple-string
    eq. > @
      "Hello, Jeffrey!"
      sprintf
        "Hello, %s!"
        * "Jeffrey"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-prints-complex-string
    eq. > @
      "Привет 3.140000 Jeff X!"
      sprintf
        "Привет %f %s %s!"
        * 3.14 "Jeff" "X"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-prints-bytes-string
    eq. > @
      "40-45-00-00-00-00-00-00"
      sprintf
        "%x"
        * 42.as-bytes

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-formats-all-objects
    eq. > @
      "string Jeff, bytes 4A-65-66-66, float 42.300000, int 14, bool false"
      sprintf
        "string %s, bytes %x, float %f, int %d, bool %b"
        * "Jeff" "Jeff".as-bytes 42.3 14 false

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> throws-on-sprintf-with-arguments-that-does-not-match
    sprintf > @
      "%s%s"
      * "Jeff"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-sprintf-does-not-fail-if-arguments-more-than-formats
    eq. > @
      "Hey"
      sprintf
        "%s"
        * "Hey" "Jeff"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> throws-on-sprintf-unsupported-format
    sprintf "%o" * > @

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-sprintf-prints-percents
    eq. > @
      "%Jeff"
      sprintf
        "%%%s"
        * "Jeff"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-sprintf-does-not-print-i64
    not. > @
      eq.
        sprintf
          "%d"
          * 42.as-i64
        "42"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-sprintf-prints-i64-as-number
    eq. > @
      "42"
      sprintf
        "%d"
        * 42.as-i64.as-number
