+alias org.eolang.txt.sprintf
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.57.3
+rt node eo2js-runtime:0.0.0
+version 0.57.3
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:19
+unlint redundant-object:20
+unlint redundant-object:21
+unlint redundant-object:22

# The 16 bits signed integer.
# Here `as-bytes` must be a `bytes` object.
[as-bytes] > i16
  as-bytes > @
  $ > as-i16
  times -1.as-i64.as-i32.as-i16 > neg
  as-i32.as-i64 > as-i64
  as-i64.as-number > as-number

  # Convert this `org.eolang.i16` to `org.eolang.i32` and return it.
  # The object is an atom because it's not possible to check what
  # bytes should be added from left so the number is valid.
  # The different bytes should be added if number is positive and negative.
  [] > as-i32 ?

  # Returns `org.eolang.true` if `$` < `x`.
  # Here `x` must be an `org.eolang.i16` object.
  as-i32.lt x.as-i16.as-i32 > [x] > lt

  # Returns `org.eolang.true` if `$` <= `x`.
  # Here `x` must be an `org.eolang.i16` object.
  as-i32.lte x.as-i16.as-i32 > [x] > lte

  # Returns `org.eolang.true` if `$` > `x`.
  # Here `x` must be an `org.eolang.i16` object.
  as-i32.gt x.as-i16.as-i32 > [x] > gt

  # Returns `org.eolang.true` if `$` >= `x`.
  # Here `x` must be an `org.eolang.i16` object.
  as-i32.gte x.as-i16.as-i32 > [x] > gte

  # Multiplication of `$` and `x`.
  # Here `x` must be an `org.eolang.i16` object.
  [x] > times
    if. > @
      or.
        left.eq 00-00
        left.eq FF-FF
      i16 right
      plus.
        i16 left
        i16 right
    (as-i32.times x.as-i16.as-i32).as-bytes > bts
    bts.slice 0 2 > left
    bts.slice 2 2 > right

  # Sum of `$` and `x`.
  # Here `x` must be an `org.eolang.i16` object.
  [x] > plus
    if. > @
      or.
        left.eq 00-00
        left.eq FF-FF
      i16 right
      plus.
        i16 left
        i16 right
    (as-i32.plus x.as-i16.as-i32).as-bytes > bts
    bts.slice 0 2 > left
    bts.slice 2 2 > right

  # Subtraction between `$` and `x`.
  # Here `x` must be an `org.eolang.i16` object.
  plus x.as-i16.neg > [x] > minus

  # Quotient of the division of `$` by `x`.
  # Here `x` must be an `org.eolang.i16` object.
  # An `org.eolang.error` is returned if `x` is equal to `0`.
  [x] > div
    if. > @
      x-as-i16.eq zero
      error
        sprintf
          "Can't divide %d by i16 zero"
          * as-i32.as-i64.as-number
      if.
        or.
          left.eq zero
          left.eq FF-FF
        i16 right
        plus.
          i16 left
          i16 right
    x.as-i16 > x-as-i16
    (as-i32.div x-as-i16.as-i32).as-bytes > bts
    bts.slice 0 2 > left
    bts.slice 2 2 > right
    00-00 > zero

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-has-valid-bytes
    eq. > @
      42.as-i64.as-i32.as-i16.as-bytes
      00-2A

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-negative-i16-has-valid-bytes
    eq. > @
      -200.as-i64.as-i32.as-i16.as-bytes
      FF-38

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-less-true
    lt. > @
      10.as-i64.as-i32.as-i16
      50.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-less-equal
    not. > @
      lt.
        10.as-i64.as-i32.as-i16
        10.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-less-false
    not. > @
      lt.
        10.as-i64.as-i32.as-i16
        -5.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-greater-true
    gt. > @
      -200.as-i64.as-i32.as-i16
      -1000.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-greater-false
    not. > @
      gt.
        0.as-i64.as-i32.as-i16
        100.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-greater-equal
    not. > @
      gt.
        0.as-i64.as-i32.as-i16
        0.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-lte-true
    lte. > @
      -200.as-i64.as-i32.as-i16
      -100.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-lte-equal
    lte. > @
      50.as-i64.as-i32.as-i16
      50.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-lte-false
    not. > @
      lte.
        0.as-i64.as-i32.as-i16
        -10.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-gte-true
    gte. > @
      -1000.as-i64.as-i32.as-i16
      -1100.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-gte-equal
    gte. > @
      113.as-i64.as-i32.as-i16
      113.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-gte-false
    not. > @
      gte.
        0.as-i64.as-i32.as-i16
        10.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-zero-eq-to-i16-zero
    eq. > @
      0.as-i64.as-i32.as-i16
      0.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-eq-true
    eq. > @
      123.as-i64.as-i32.as-i16
      123.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-eq-false
    not. > @
      eq.
        123.as-i64.as-i32.as-i16
        42.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-one-plus-i16-one
    eq. > @
      1.as-i64.as-i32.as-i16.plus 1.as-i64.as-i32.as-i16
      2.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-plus-with-overflow
    eq. > @
      32767.as-i64.as-i32.as-i16.plus 1.as-i64.as-i32.as-i16
      -32768.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-one-minus-i16-one
    eq. > @
      1.as-i64.as-i32.as-i16.minus 1.as-i64.as-i32.as-i16
      0.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-minus-with-overflow
    eq. > @
      -32768.as-i64.as-i32.as-i16.minus 1.as-i64.as-i32.as-i16
      32767.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  2.as-i64.as-i32.as-i16.div 0.as-i64.as-i32.as-i16 > [] +> throws-on-division-i16-by-i16-zero

  # This unit test is supposed to check the functionality of the corresponding object.
  # Checks that division by one returns the dividend.
  [] +> tests-i16-div-by-i16-one
    eq. > @
      dividend.div 1.as-i64.as-i32.as-i16
      dividend
    -235.as-i64.as-i32.as-i16 > dividend

  # This unit test is supposed to check the functionality of the corresponding object.
  # Checks div with remainder.
  [] +> tests-i16-div-with-remainder
    eq. > @
      13.as-i64.as-i32.as-i16.div -5.as-i64.as-i32.as-i16
      -2.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-div-less-than-i16-one
    lt. > @
      1.as-i64.as-i32.as-i16.div 5.as-i64.as-i32.as-i16
      1.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-multiply-by-zero
    eq. > @
      1000.as-i64.as-i32.as-i16.times 0.as-i64.as-i32.as-i16
      0.as-i64.as-i32.as-i16

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-i16-times-with-overflow
    eq. > @
      32767.as-i64.as-i32.as-i16.times 2.as-i64.as-i32.as-i16
      -2.as-i64.as-i32.as-i16
