+alias org.eolang.fs.dir
+alias org.eolang.fs.file
+alias org.eolang.sm.os
+alias org.eolang.ss.list
+alias org.eolang.ss.reduced
+alias org.eolang.tt.regex
+alias org.eolang.tt.index-of
+alias org.eolang.tt.last-index-of
+alias org.eolang.tt.replaced
+alias org.eolang.tt.split
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.fs
+version 0.59.5
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:56
+unlint redundant-object:58
+unlint redundant-object:59
+unlint redundant-object:268
+unlint redundant-object:270
+unlint redundant-object:271

# A `path` represents a path that is hierarchical and composed of a sequence of
# directory and file name elements separated by a special separator or delimiter.
[uri] > path
  if. > @
    os.is-windows
    win32
      string uri.as-bytes
    posix
      string uri.as-bytes
  # The system-dependent default name-separator character.
  # On UNIX systems the value of this field is "/";
  # on Microsoft Windows systems it is "\\".
  if. > separator
    os.is-windows
    win32.separator
    posix.separator

  # Utility object that joins given `tuple` of paths with current OS separator
  # and normalizes result path.
  [paths] > joined
    normalized. > @
      if.
        os.is-windows
        win32 joined-path
        posix joined-path
    string > joined-path
      as-bytes.
        QQ.tt.joined
          separator
          paths

  # POSIX specified path.
  # A standardized way to represent file or directory locations in a Unix-like system.
  [uri] > posix
    uri > @
    "/" > separator
    file uri > as-file
    dir (file uri) > as-dir
    # Returns `true` if current path is absolute - starts with '/' char.
    and. > is-absolute
      uri.length.gt 0
      (uri.as-bytes.slice 0 1).eq separator

    # Return new `path` with normalized uri.
    # Normalization includes:
    # - converting multiple slashes into a single slash.
    # - resolving "." (current directory) and ".."  (parent directory) segments.
    [] > normalized
      posix > @
        if.
          normalized.eq "//"
          "/"
          string normalized
      uri > uri-as-bytes!
      ^.is-absolute.as-bool > is-absolute
      and. > has-trailing-slash
        uri-as-bytes.size.gt 0
        eq.
          uri-as-bytes.slice
            uri-as-bytes.size.plus -1
            1
          separator
      QQ.tt.joined > path
        separator
        reduced
          list
            split
              uri
              separator
          *
          [accum segment] >>
            if. > @
              segment.eq ".."
              if.
                and.
                  accum.length.gt 0
                  (accum.head.eq "..").not
                accum.tail
                if.
                  is-absolute.not
                  accum.with segment
                  accum
              if.
                or.
                  segment.eq "."
                  segment.eq ""
                accum
                accum.with segment
      as-bytes. > normalized
        if.
          uri.length.eq 0
          "."
          concat.
            if.
              is-absolute
              separator.concat path
              path
            if.
              has-trailing-slash
              separator
              --

    # Resolves `other` path against `^.uri` and returns as new `path` from it.
    # The original `uri` and resolving `other` may be:
    # - absolute - start with unix separator '/'
    # - relative - start with '.', '..' or any letter
    #
    # The resolving works as follows:
    # |----------|----------|-------------|
    # | uri      | other    | result      |
    # |==========|==========|=============|
    # | absolute | absolute | other       |
    # | absolute | relative | uri + other |
    # | relative | absolute | other       |
    # | relative | relative | uri + other |
    # |----------|----------|-------------|
    # URI Resolution Rules.
    [other] > resolved
      normalized. > @
        posix
          string
            if.
              (other-as-bytes.slice 0 1).eq separator
              other-as-bytes
              concat.
                concat.
                  uri
                  separator
                other-as-bytes
      other.as-bytes > other-as-bytes

    # Takes the base name from the provided `path`, for example:
    # |------------------------------|---------------|
    # | path                         | result        |
    # |==============================|===============|
    # | "/tmp/bar/foo.txt"           | "foo.txt"     |
    # | "/tmp/bar/foo/"              | ""            |
    # | "/var/www/html"              | "html"        |
    # | "Hello, Jeff"                | "Hello, Jeff" |
    # | ""                           | ""            |
    # |------------------------------|---------------|
    #
    # Unix OS allows having "\" (backslash) in file names.
    # In such case it'll be included to the base name since separator
    # is "/" (regular slash) in Unix OS:
    # With the `path` "/tmp/var/hello\world" `base-name` returns `hello\world`.
    [] > basename
      string > @
        if.
          or.
            pth.size.eq 0
            slice-start-idx.eq 0
          pth
          as-bytes.
            txt.slice
              slice-start-idx
              txt.length.minus
                number slice-start-idx
      uri > pth!
      string pth > txt
      plus. > slice-start-idx!
        last-index-of txt separator
        1

    # Takes the extension part from the provided `path`, for example:
    # |------------------------------|--------|
    # | path                         | result |
    # |==============================|========|
    # | "/tmp/bar/foo.txt"           | ".txt" |
    # | "/tmp/bar/foo/"              | ""     |
    # | /var/www/html                | ""     |
    # | "Hello, Jeff"                | ""     |
    # | ""                           | ""     |
    # |------------------------------|--------|
    #
    # The object takes the `basename` from the provided path
    # and returns substring after "." if it's found.
    # If it's not - empty string is returned.
    [] > extname
      if. > @
        or.
          base.size.eq 0
          slice-start-idx.eq -1
        ""
        string
          as-bytes.
            txt.slice
              slice-start-idx
              txt.length.minus
                number slice-start-idx
      basename > base!
      string base > txt
      last-index-of txt "." > slice-start-idx!

    # Takes the directory name from the provided `path`, for example:
    # |------------------------------|--------------------|
    # | path                         | result             |
    # |==============================|====================|
    # | "/tmp/bar/foo.txt"           | "/tmp/bar"         |
    # | "/tmp/bar/foo/"              | "/tmp/bar/foo"     |
    # | "/var/www/html"              | "/var/www"         |
    # | "C:\Windows\Users\Error.log" | "C:\Windows\Users" |
    # | "Hello, Jeff"                | "Hello, Jeff"      |
    # | ""                           | ""                 |
    # |------------------------------|--------------------|
    #
    # Unix OS allows having "\" (backslash) in file or directory names.
    # In such case it'll be included to the dir name since separator
    # is "/" (regular slash) in Unix OS:
    # With the `path` "/tmp/v\a\r/hello" `dir-name` returns `/tmp/v\a\r`.
    [] > dirname
      string > @
        if.
          or.
            pth.size.eq 0
            len.eq -1
          pth
          as-bytes.
            txt.slice 0 len
      uri > pth!
      string pth > txt
      last-index-of txt separator > len!

  # Windows specified path.
  # Here `uri` is file system URI which may contain forward slashes '/'.
  # The object decorates `win32.validated` with forward slashes replaced with
  # back ones.
  [uri] > win32
    validated > @
      string
        as-bytes.
          validated.separated-correctly
            uri
    "\\" > separator

    # Windows validated path with forward slashes replaced with back ones.
    #
    # Note: This object is intended for internal use only. Please
    # don't use the object programmatically outside of `path` object.
    [uri] > validated
      uri > @
      ^.separator > separator
      file uri > as-file
      dir (file uri) > as-dir

      # Returns `true` if given `uri` is drive relative which means it
      # starts with "X:" where 'X' - system drive name.
      ((regex "/^[a-zA-Z]:/").matches uri).as-bool > [uri] > is-drive-relative

      # Returns `true` if given `uri` is root relative which means it
      # starts with windows separator '\'.
      [uri] > is-root-relative
        and. > @
          uri-as-bytes.size.gt 0
          (uri-as-bytes.slice 0 1).eq separator
        uri > uri-as-bytes!

      # Replaces slashes '/' with valid windows separator '\'.
      [uri] > separated-correctly
        if. > @
          (index-of pth path.posix.separator).eq -1
          string uri-as-bytes
          string replaced-slash
        uri > uri-as-bytes!
        string uri-as-bytes > pth
        replaced > replaced-slash!
          pth
          regex "/\\//"
          separator

      # Returns `true` if current path is absolute, which means:
      # - either path root is root relative (starts with "\")
      # - or path is drive relative (starts with "X:\", where 'X' - 1 char disk name).
      [] > is-absolute
        if. > @
          uri-as-bytes.size.eq 0
          false
          or.
            is-root-relative uri-as-bytes
            and.
              uri-as-bytes.size.gt 1
              is-drive-relative uri-as-bytes
        uri > uri-as-bytes!

      # Return new `path` with normalized uri.
      # Normalization includes:
      # - converting multiple slashes into a single slash.
      # - resolving "." (current directory) and ".."  (parent directory) segments.
      # - handling of drive letters in Windows.
      [] > normalized
        validated > @
          if.
            normalized.eq "\\\\"
            separator
            string normalized
        uri > uri-as-bytes!
        (^.is-drive-relative uri-as-bytes).as-bool > is-drive-relative
        (^.is-root-relative uri-as-bytes).as-bool > is-root-relative
        if. > driveless!
          is-drive-relative
          uri-as-bytes.slice
            2
            uri-as-bytes.size.plus -2
          uri-as-bytes
        and. > has-trailing-slash
          uri-as-bytes.size.gt 0
          eq.
            uri-as-bytes.slice
              uri-as-bytes.size.plus -1
              1
            separator
        QQ.tt.joined > path!
          separator
          reduced
            list
              split
                driveless
                ^.separator
            *
            [accum segment] >>
              if. > @
                segment.eq ".."
                if.
                  and.
                    accum.length.gt 0
                    (accum.head.eq "..").not
                  accum.tail
                  if.
                    and.
                      is-root-relative.not
                      is-drive-relative.not
                    accum.with segment
                    accum
                if.
                  or.
                    segment.eq "."
                    segment.eq ""
                  accum
                  accum.with segment
        as-bytes. > normalized
          if.
            driveless.size.eq 0
            "."
            concat.
              concat.
                if.
                  is-drive-relative
                  if.
                    (driveless.slice 0 1).eq separator
                    uri.slice 0 3
                    uri.slice 0 2
                  if.
                    is-root-relative
                    separator
                    --
                path
              if.
                has-trailing-slash
                separator
                --

      # Resolves `other` path against `^.uri` and returns as new `path` from it.
      # Original `uri` and `other` path for resolving may be:
      # - drive relative - start from "X:" where 'X' - system drive name
      # - root relative - start from windows separator '\'
      # - any - start from '.', '..' or any letter
      #
      # Accounting these options resolving work as follows:
      # |--------------------|----------------|-------------------|
      # | uri                | other          | result            |
      # |====================|================|===================|
      # | any                | any            | uri + other       |
      # | any                | drive relative | other             |
      # | any                | root relative  | other             |
      # | drive relative     | any            | uri + other       |
      # | drive relative     | drive relative | other             |
      # | drive relative     | root relative  | uri drive + other |
      # | root relative      | any            | uri + other       |
      # | root relative      | drive relative | other             |
      # | root relative      | root relative  | other             |
      # |--------------------|----------------|-------------------|
      # URI Resolution Rules.
      [other] > resolved
        normalized. > @
          validated
            string
              if.
                (^.is-drive-relative valid-other).as-bool
                valid-other
                if.
                  (^.is-root-relative valid-other).as-bool
                  if.
                    is-drive-relative uri-as-bytes
                    concat.
                      uri-as-bytes.slice 0 2
                      valid-other
                    valid-other
                  concat.
                    concat.
                      uri-as-bytes
                      separator
                    valid-other
        uri > uri-as-bytes!
        separated-correctly other > valid-other!

      # Takes the base name from the provided `path`, for example:
      # |------------------------------|---------------|
      # | path                         | result        |
      # |==============================|===============|
      # | "/tmp/bar/foo.txt"           | "foo.txt"     |
      # | "/tmp/bar/foo/"              | ""            |
      # | "/var/www/html"              | "html"        |
      # | "Hello, Jeff"                | "Hello, Jeff" |
      # | ""                           | ""            |
      # |------------------------------|---------------|
      #
      # Unix OS allows having "\" (backslash) in file names.
      # In such case it'll be included to the base name since separator
      # is "/" (regular slash) in Unix OS:
      # With the `path` "/tmp/var/hello\world" `base-name` returns `hello\world`.
      [] > basename
        string > @
          if.
            or.
              pth.size.eq 0
              slice-start-idx.eq 0
            pth
            as-bytes.
              txt.slice
                slice-start-idx
                txt.length.minus
                  number slice-start-idx
        uri > pth!
        string pth > txt
        plus. > slice-start-idx!
          last-index-of txt separator
          1

      # Takes the extension part from the provided `path`, for example:
      # |------------------------------|--------|
      # | path                         | result |
      # |==============================|========|
      # | "/tmp/bar/foo.txt"           | ".txt" |
      # | "/tmp/bar/foo/"              | ""     |
      # | /var/www/html                | ""     |
      # | "Hello, Jeff"                | ""     |
      # | ""                           | ""     |
      # |------------------------------|--------|
      #
      # The object takes the `basename` from the provided path
      # and returns substring after "." if it's found.
      # If it's not - empty string is returned.
      [] > extname
        if. > @
          or.
            base.size.eq 0
            slice-start-idx.eq -1
          ""
          string
            as-bytes.
              txt.slice
                slice-start-idx
                txt.length.minus
                  number slice-start-idx
        basename > base!
        string base > txt
        last-index-of txt "." > slice-start-idx!

      # Takes the directory name from the provided `path`, for example:
      # |------------------------------|--------------------|
      # | path                         | result             |
      # |==============================|====================|
      # | "/tmp/bar/foo.txt"           | "\tmp\bar"         |
      # | "\tmp\bar\foo\"              | "\tmp\bar\foo"     |
      # | "/var/www/html"              | "\var\www"         |
      # | "C:\Windows\Users\Error.log" | "C:\Windows\Users" |
      # | "Hello, Jeff"                | "Hello, Jeff"      |
      # | ""                           | ""                 |
      # |------------------------------|--------------------|
      # Extracting Directory Names from File Paths.
      [] > dirname
        string > @
          if.
            or.
              pth.size.eq 0
              len.eq -1
            pth
            as-bytes.
              txt.slice 0 len
        uri > pth!
        string pth > txt
        last-index-of txt separator > len!

  # Tests that the path separator is determined correctly based on the operating system.
  [] +> tests-determines-separator-depending-on-os
    eq. > @
      path.separator
      if.
        os.is-windows
        path.win32.separator
        path.posix.separator

  # Tests that absolute and relative POSIX paths are correctly identified.
  [] +> tests-detects-absolute-posix-path
    and. > @
      (path.posix "/var/www/html").is-absolute
      (path.posix "foo/bar/baz").is-absolute.not

  # Tests that absolute and relative Windows paths are correctly identified.
  [] +> tests-detects-absolute-win32-path
    and. > @
      and.
        (path.win32 "C:\\Windows\\Users").is-absolute
        (path.win32 "\\Windows\\Users").is-absolute
      (path.win32 "temp\\var").is-absolute.not

  # Tests that POSIX path normalization resolves dots and double slashes correctly.
  [] +> tests-normalizes-posix-path
    eq. > @
      (path.posix "/foo/bar/.//./baz//../x/").normalized
      "/foo/bar/x/"

  # Tests that relative POSIX path normalization handles parent directory references.
  [] +> tests-normalizes-posix-relative-path
    eq. > @
      (path.posix "../../foo/./bar/../x/../y").normalized
      "../../foo/y"

  # Tests that normalizing an empty POSIX path returns current directory.
  [] +> tests-normalizes-empty-posix-path-to-current-dir
    eq. > @
      (path.posix "").normalized
      "."

  # Tests that normalizing a path with excessive parent references resolves to root.
  [] +> tests-normalizes-path-to-root
    eq. > @
      (path.posix "/foo/bar/baz/../../../../").normalized
      "/"

  # Tests that Windows path normalization works for absolute paths without drive letters.
  [] +> tests-normalizes-absolute-win32-path-without-drive
    eq. > @
      (path.win32 "\\Windows\\Users\\..\\App\\\\.\\Local\\\\").normalized
      "\\Windows\\App\\Local\\"

  # Tests that Windows path normalization works for absolute paths with drive letters.
  [] +> tests-normalizes-absolute-win32-path-with-drive
    eq. > @
      (path.win32 "C:\\Windows\\\\..\\Users\\.\\AppLocal").normalized
      "C:\\Users\\AppLocal"

  # Tests that Windows relative path normalization handles parent directory references.
  [] +> tests-normalizes-relative-win32-path
    eq. > @
      (path.win32 "..\\..\\foo\\bar\\..\\x\\y\\\\").normalized
      "..\\..\\foo\\x\\y\\"

  # Tests that normalizing an empty Windows path returns current directory.
  [] +> tests-normalizes-empty-win32-driveless-path-to-current-dir
    eq. > @
      (path.win32 "").normalized
      "."

  # Tests that Windows path normalization to drive root includes trailing separator.
  [] +> tests-normalizes-win32-path-down-to-drive-with-separator
    eq. > @
      (path.win32 "C:\\Windows\\..").normalized
      "C:\\"

  # Tests that Windows path normalization to drive root without separator works correctly.
  [] +> tests-normalizes-win32-path-down-to-drive-without-separator
    eq. > @
      (path.win32 "C:hello\\..").normalized
      "C:"

  # Tests that Windows path normalization replaces forward slashes with backslashes.
  [] +> tests-normalizes-win32-path-with-replacing-slashes
    eq. > @
      (path.win32 "/var/www/../html/").normalized
      "\\var\\html\\"

  # Tests that resolving an absolute POSIX path against another absolute path returns the second path.
  [] +> tests-resolves-posix-absolute-path-against-other-absolute-path
    eq. > @
      (path.posix "/var/temp").resolved "/www/html"
      "/www/html"

  # Tests that resolving an absolute POSIX path against a relative path concatenates them.
  [] +> tests-resolves-posix-absolute-path-against-other-relative-path
    eq. > @
      (path.posix "/var/temp").resolved "./www/html"
      "/var/temp/www/html"

  # Tests that resolving a relative POSIX path against an absolute path returns the absolute path.
  [] +> tests-resolves-posix-relative-path-against-other-absolute-path
    eq. > @
      (path.posix "./var/temp").resolved "/www/html"
      "/www/html"

  # Tests that resolving a relative POSIX path against another relative path works correctly.
  [] +> tests-resolves-posix-relative-path-against-other-relative-path
    eq. > @
      (path.posix "./var/temp").resolved "../www/html"
      "var/www/html"

  # Tests that resolving a relative Windows path against another relative path works correctly.
  [] +> tests-resolves-win32-relative-path-against-other-relative-path
    eq. > @
      (path.win32 ".\\temp\\var").resolved ".\\..\\x"
      "temp\\x"

  # Tests that resolving a relative Windows path against a drive-relative path returns the drive path.
  [] +> tests-resolves-win32-relative-path-against-other-drive-relative-path
    eq. > @
      (path.win32 ".\\temp\\var").resolved "C:\\Windows\\Users"
      "C:\\Windows\\Users"

  # Tests that resolving a relative Windows path against a root-relative path returns the root path.
  [] +> tests-resolves-win32-relative-path-against-other-root-relative-path
    eq. > @
      (path.win32 ".\\temp\\var").resolved "\\Windows\\Users"
      "\\Windows\\Users"

  # Tests that resolving a drive-relative Windows path against a relative path concatenates them.
  [] +> tests-resolves-win32-drive-relative-path-against-other-relative-path
    eq. > @
      (path.win32 "C:\\users\\local").resolved ".\\var\\temp"
      "C:\\users\\local\\var\\temp"

  # Tests that resolving a drive-relative Windows path against another path returns the second path.
  [] +> tests-resolves-win32-drive-relative-path-against-other-drive-relative-path
    eq. > @
      (path.win32 "C:\\users\\local").resolved "D:\\local\\var"
      "D:\\local\\var"

  # Tests that resolving a drive-relative Windows path against a root path combines them correctly.
  [] +> tests-resolves-win32-drive-relative-path-against-other-root-relative-path
    eq. > @
      (path.win32 "C:\\users\\local").resolved "\\local\\var"
      "C:\\local\\var"

  # Tests that resolving a root-relative Windows path against a relative path concatenates them.
  [] +> tests-resolves-win32-root-relative-path-against-other-relative-path
    eq. > @
      (path.win32 "\\users\\local").resolved ".\\hello\\var"
      "\\users\\local\\hello\\var"

  # Tests that resolving a root-relative Windows path against a drive path returns the drive path.
  [] +> tests-resolves-win32-root-relative-path-against-other-drive-relative-path
    eq. > @
      (path.win32 "\\users\\local").resolved "D:\\hello\\var"
      "D:\\hello\\var"

  # Tests that resolving a root-relative Windows path against another root path returns the second path.
  [] +> tests-resolves-win32-root-relative-path-against-other-root-relative-path
    eq. > @
      (path.win32 "\\users\\local").resolved "\\hello\\var"
      "\\hello\\var"

  # Tests that extracting the basename from a file path returns the correct filename.
  [] +> tests-takes-valid-basename
    eq. > @
      basename.
        path.joined
          * "var" "www" "html" "hello.eo"
      "hello.eo"

  # Tests that extracting basename from a path ending with separator returns empty string.
  [] +> tests-returns-empty-basename-from-path-ended-with-separator
    eq. > @
      basename.
        path.joined
          * "var" "www" "html" path.separator
      ""

  # Tests that POSIX basename extraction preserves backslashes in filenames.
  [] +> tests-returns-base-with-backslash-in-path-on-posix
    eq. > @
      (path.posix "/var/www/html/foo\\bar").basename
      "foo\\bar"

  # Tests that basename extraction returns the original string when no separator is found.
  [] +> tests-returns-the-same-string-if-no-separator-is-found
    eq. > @
      (path "Somebody").basename
      "Somebody"

  # Tests that extracting file extension from a path returns the correct extension.
  [] +> tests-takes-file-extname
    eq. > @
      extname.
        path.joined
          * "var" "www" "hello.txt"
      ".txt"

  # Tests that extracting extension from a file without extension returns empty string.
  [] +> tests-does-not-take-extname-on-file-without-extension
    eq. > @
      extname.
        path.joined
          * "var" "www" "html"
      ""

  # Tests that extracting extension from a path ending with separator returns empty string.
  [] +> tests-does-not-take-extname-if-ends-with-separator
    eq. > @
      extname.
        path.joined
          * "var" "www" path.separator
      ""

  # Tests that extracting directory name from a file path returns the parent directory.
  [] +> tests-returns-valid-dirname-from-file-path
    eq. > @
      dirname.
        path.joined
          * "var" "www" "hello.txt"
      path.joined
        * "var" "www"

  # Tests that extracting directory name from a directory path returns the parent directory.
  [] +> tests-returns-valid-dirname-from-dir-path
    eq. > @
      dirname.
        path.joined
          * "var" "www" path.separator
      path.joined
        * "var" "www"

  # Tests that extracting directory name from a file path without extension works correctly.
  [] +> tests-returns-valid-dirname-from-file-path-without-extension
    eq. > @
      dirname.
        path.joined
          * "var" "www" "html"
      path.joined
        * "var" "www"
