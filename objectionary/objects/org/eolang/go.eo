# The MIT License (MIT)
#
#  2016-2022 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.37.0
+version 0.37.0

# Non-conditional forward and backward jumps.
# Forward jump instantly returns provided object to `g.forward` without touching
# all the code below.
# Backward jump instantly returns provided to `go.to` abstract object
#
# Forward jump may look like this:
#
# ```
# go.to
#   [g]
#     seq > @
#       *
#         g.forward true
#         stdout "Will never be printed"
# ```
#
# Backward jump may be done this way:
#
# ```
# go.to
#   [g]
#     seq > @
#       *
#         stdout "Will be printed forever"
#         g.backward
# ```
# Go to.
[] > go
  malloc.of > id!
    8
    m.put m.id > [m]

  # To.
  [body] > to
    try > @
      body token
      [e]
        if. > @
          &.^.id.eq e.id
          e.value
          error e
      true

    # Token.
    [] > token
      # Backward jump.
      error > backward
        []
          &.^.^.to &.^.body > value
          &.^.^.id > id

      # Forward jump.
      [res] > forward
        error > @
          []
            res > value
            &.^.^.^.id > id
