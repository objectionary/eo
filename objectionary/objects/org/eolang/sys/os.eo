+alias org.eolang.txt.regex
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.sys
+rt jvm org.eolang:eo-runtime:0.58.1
+rt node eo2js-runtime:0.0.0
+version 0.58.1
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:23
+unlint redundant-object:24

# Represents the current operating system.
[] > os
  name > @

  # Returns `org.eolang.true` if current OS belongs to `Windows` OS family.
  [] > is-windows
    and. > @
      os-name.size.gt 6
      (os-name.slice 0 7).eq "Windows"
    name > os-name!
  ((regex "/linux/i").matches name).as-bool > is-linux
  ((regex "/mac/i").matches name).as-bool > is-macos

  # Returns the name of the current operation system as `org.eolang.string`.
  [] > name ?

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-checks-os-family
    or. > @
      or.
        os.is-windows
        os.is-macos
      os.is-linux
