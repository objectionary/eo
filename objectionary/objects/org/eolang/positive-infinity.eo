+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.55.2
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Positive infinity.
# A special floating-point value representing an unbounded quantity greater than all real numbers.
# When dataized, it signifies an unbounded upper limit or an unreachable maximum value.
[] > positive-infinity
  number 7F-F0-00-00-00-00-00-00 > @
  $ > floor
  negative-infinity > neg
  false > is-nan
  false > is-finite
  false > is-integer
  error "Can't convert positive infinity to i64" > as-i64

  # Tests that the value $ is equal to x.
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that the value $ less than x.
  false > [x] > lt

  # Tests that the value $ less or equal than x.
  eq x > [x] > lte

  # Tests that the value $ greater than x.
  [x] > gt
    x > value!
    not. > @
      or.
        (number value).is-nan
        eq value

  # Tests that the value $ greater or equal than x.
  [x] > gte
    x > value!
    not. > @
      (number value).is-nan

  # Returns the result of the multiplication of $ and x.
  [x] > times
    x > value!
    number value > num
    if. > @
      or.
        num.is-nan
        num.eq 0
      nan
      if.
        num.gt 0
        ^
        negative-infinity

  # Returns the result of the sum of $ and x.
  [x] > plus
    x > value!
    if. > @
      or.
        (number value).is-nan
        value.eq negative-infinity
      nan
      ^

  # Difference between the values of $ and x.
  [x] > minus
    x > value!
    if. > @
      or.
        (number value).is-nan
        value.eq ^
      nan
      ^

  # Quotient of the division of $ by x.
  [x] > div
    x > value!
    number value > num
    if. > @
      or.
        num.is-nan
        num.is-finite.not
      nan
      if.
        or.
          value.eq -0.as-bytes
          0.gt value
        negative-infinity
        ^
