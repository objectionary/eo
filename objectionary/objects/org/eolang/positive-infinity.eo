# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.39.0
+rt node eo2js-runtime:0.0.0
+version 0.39.0

# Positive infinity.
[] > positive-infinity
  1.0.div 0.0 > @

  # Tests that $ = x.
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that $ < x.
  [x] > lt
    false > @

  # Tests that $ â‰¤ x.
  [x] > lte
    ^.eq x > @

  # Tests that $ > x.
  [x] > gt
    x > value!
    not. > @
      or.
        value.eq nan.as-bytes
        ^.eq value

  # Tests that $ â‰¥ x.
  [x] > gte
    not. > @
      eq.
        x.as-bytes
        nan.as-bytes

  # Multiplication of $ and x.
  [x] > times
    x > value!
    if. > @
      is-nan-or-zero value
      nan
      if.
        0.0.lt value
        ^
        negative-infinity

    num.eq nan.as-bytes > [num] > is-nan

    [num] > is-nan-or-zero
      or. > @
        or.
          is-nan num
          num.eq -0.0
        num.eq 0.0

  # Sum of $ and x.
  [x] > plus
    x > value!
    if. > @
      or.
        is-nan value
        value.eq negative-infinity
      nan
      ^

    num.eq nan.as-bytes > [num] > is-nan

  # Difference between $ and x.
  [x] > minus
    x > value!
    if. > @
      or.
        is-nan value
        value.eq ^
      nan
      ^

    num.eq nan.as-bytes > [num] > is-nan

  # Quotient of the division of $ by x.
  [x] > div
    x > value!
    if. > @
      is-nan-or-infinite value
      nan
      if.
        or.
          value.eq -0.0.as-bytes
          0.0.gt value
        negative-infinity
        ^

    num.eq nan.as-bytes > [num] > is-nan

    [num] > is-nan-or-infinite
      or. > @
        or.
          is-nan num
          num.eq ^.^
        num.eq negative-infinity

