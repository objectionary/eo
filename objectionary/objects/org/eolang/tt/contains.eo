+alias org.eolang.tt.slice
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.59.4
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Checks if `text` contains `substring`.
[text substring] > contains
  and. > @
    (number text-size).gte substring-size
    or.
      and.
        text-size.eq substring-size
        text-bts.eq substring-bts
      rec-contains 0
  text > text-bts!
  substring > substring-bts!
  text-bts.size > text-size!
  substring-bts.size > substring-size!
  (number text-size).minus substring-size > end!

  [idx] > rec-contains
    if. > @
      end.eq idx
      includes
      or.
        includes
        rec-contains (idx.plus 1).as-number
    eq. > includes
      slice text-bts idx substring-size
      substring-bts

  # Tests specific functionality contains returns true when text includes given substring.
  [] +> tests-text-contains-substring
    contains "Hello, world!" "o, wo" > @

  # Tests specific functionality contains returns false when text does not include substring.
  [] +> tests-text-does-not-contain-substring
    not. > @
      contains "Hello, world!" "Hey"
