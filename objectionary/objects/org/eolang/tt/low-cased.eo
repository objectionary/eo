+alias org.eolang.tt.up-cased
+alias org.eolang.ss.list
+alias org.eolang.ss.bytes-as-array
+alias org.eolang.ss.reduced
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.59.5
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Returns the `text` in lower case.
[text] > low-cased
  string > @
    reduced
      list
        bytes-as-array
          text.as-bytes
      --
      [accum byte] >>
        accum.concat > @
          if.
            and.
              ascii-bte.lte ascii-z
              ascii-bte.gte ascii-a
            slice.
              as-bytes.
                as-i64.
                  ascii-bte.plus (up-cased text).distance
              7
              1
            byte
        (up-cased text).ascii byte > ascii-bte
  (up-cased text).ascii "Z" > ascii-z
  (up-cased text).ascii "A" > ascii-a

  # Tests that low-cased converts uppercase text to lowercase.
  [] +> tests-converts-text-to-lower-case
    eq. > @
      "hello"
      low-cased "HELLO"

  # Tests that low-cased converts mixed case text to lowercase.
  [] +> tests-converts-text-with-low-letters-to-lower-case
    eq. > @
      "hello"
      low-cased "HeLlO"
