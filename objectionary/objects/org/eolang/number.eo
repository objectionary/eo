# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.39.0
+rt node eo2js-runtime:0.0.0
+version 0.39.0

# The `number` object is an abstraction of a 64-bit floating-point
# number that internally is a chain of eight bytes.
[as-bytes] > number
  as-bytes > @

  # Tests that $ = x.
  [x] > eq
    x > x-as-bytes!
    nan > nan-as-bytes!
    ^.as-bytes > self-as-bytes
    0.0 > pos-zero-as-bytes!
    -0.0 > neg-zero-as-bytes!
    if. > @
      or.
        x-as-bytes.eq nan-as-bytes
        self-as-bytes.eq nan-as-bytes
      false
      or.
        and.
          or.
            x-as-bytes.eq pos-zero-as-bytes
            x-as-bytes.eq neg-zero-as-bytes
          or.
            self-as-bytes.eq pos-zero-as-bytes
            self-as-bytes.eq neg-zero-as-bytes
        self-as-bytes.eq x-as-bytes

  # Tests that $ < x.
  [x] > lt
    x > value!
    0.0.gt > @
      ^.minus
        number value

  # Tests that $ ≤ x.
  [x] > lte
    x > value!
    or. > @
      ^.lt value
      ^.eq value

  # Tests that $ > x.
  [x] > gt /bool

  # Tests that $ ≥ x.
  [x] > gte
    x > value!
    or. > @
      ^.gt value
      ^.eq value

  # Multiplication of $ and x.
  [x] > times /number

  # Sum of $ and x.
  [x] > plus /number

  # Negation of $.
  ^.times -1.0 > [] > neg

  # Difference between $ and x.
  ^.plus x.neg > [x] > minus

  # Quotient of the division of $ by x.
  [x] > div /number

  # The object rounds down the original `number`  to the nearest
  # whole `number` that is less than or equal to the original one.
  [] > floor /number
