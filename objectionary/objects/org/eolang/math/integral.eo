+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.math
+version 0.54.1
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Counts integral from `a` to `b`.
# Here `func` is integration function, `a` is an upper limit,
# `b` is bottom limit, `n` is the number of partitions of the integration interval.
[fun a b n] > integral
  [a b] > subsection
    times. > @
      div.
        b.minus a
        6.0
      plus.
        fun a
        plus.
          times.
            4.0
            fun
              times.
                0.5
                a.plus b
          fun b
  as-number. > @
    malloc.of
      8
      [sum] >>
        malloc.for > @
          a
          [left] >>
            b > right
            ((right.minus left).div n).as-number > step
            seq * > @
              while
                [i] >>
                  if. > @
                    (left.as-number.plus step).lt right
                    seq *
                      sum.put
                        sum.as-number.plus
                          subsection
                            left.as-number
                            left.as-number.plus step
                      left.put
                        left.as-number.plus step
                      true
                    false
                true > [i]
              sum
