# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+package org.eolang.math
+home https://github.com/objectionary/eo
+version 0.23.17
+rt jvm org.eolang:eo-runtime:0.23.17

[n] > number

  # Checking type of the number's decorator.
  # If $.n is int, return true, otherwise false.
  [] > is-int
    if. > @
      eq.
        ^.n
        0
      FALSE
      eq.
        div.
          ^.n
          ^.n
        1

  # Checking type of the number's decorator.
  # If $.n is float, return true, otherwise false.
  [] > is-float
    if. > @
      eq.
        ^.n
        0.0
      TRUE
      eq.
        div.
          ^.n
          ^.n
        1.0

  [x] > xor
    if. > @
      is-int
      as-int.
        xor.
          ^.n.as-bytes
          x.as-bytes
      []
        "xor cannot be applied to float" > msg

  [x] > or
    if. > @
      is-int
      as-int.
        or.
          ^.n.as-bytes
          x.as-bytes
      []
        "or cannot be applied to float" > msg

  [x] > and
    if. > @
      is-int
      as-int.
        and.
          ^.n.as-bytes
          x.as-bytes
      []
        "and cannot be applied to float" > msg

  # Calculate bitwise left shift
  [x] > left
    if. > @
      is-int
      as-int.
        left.
          ^.n.as-bytes
          x
      []
        "left cannot be applied to float" > msg

  # Calculate bitwise right shift
  [x] > right
    if. > @
      is-int
      as-int.
        right.
          ^.n.as-bytes
          x
      []
        "right cannot be applied to float" > msg

  # Tests that $.n â‰  x
  [x] > neq
    (^.n.eq x).not > @

  # Calculate MOD
  [x] > mod
    ^.n > dividend
    x > divisor

    if. > @
      divisor.eq 0
      []
        "mod by zero" > msg
      if.
        dividend.gt 0
        abs-mod
        neg.
          abs-mod

    [] > abs-mod
      (number dividend).abs > int-abs
      (number divisor).abs > x-abs
      minus. > @
        int-abs
        x-abs.times
          int-abs.div x-abs

  # Absolute value of $.n (i.e., with no sign)
  [] > abs
    ^.n > value
    if. > @
      is-int
      if.
        value.gte 0
        value
        value.neg
      if.
        value.gte 0.0
        value
        value.neg

  # Make ^.n power x
  [x] > pow /?

  # Converts ^.n to float
  [] > as-float /float

  # Converts this to int
  [] > as-int /int

  # Converts ^.n to string
  [] > as-string /string

  # Sign of $ (-1, 0, or 1) if $ is int
  # or (-1.0, 0.0, or 1.0) if $ is float
  [] > signum
    if. > @
      is-int
      if.
        ^.n.lt 0
        -1
        if.
          ^.n.eq 0
          0
          1
      if.
        ^.n.lt 0.0
        -1.0
        if.
          ^.n.eq 0.0
          0.0
          1.0
