+alias org.eolang.math.pi
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.math
+rt jvm org.eolang:eo-runtime:0.53.0
+rt node eo2js-runtime:0.0.0
+version 0.53.0
+unlint rt-without-atoms
+unlint empty-object
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# The angle.
# A measure of how much something is tilted or rotated, measured in degrees or radians.
# When dataized, it shows direction or how two lines meet.
[value] > angle
  value > @
  # Converts this from radians to degrees.
  angle > in-degrees
    div.
      $.times 180.0
      pi
  # Converts this from degrees to radians.
  angle > in-radians
    div.
      $.times pi
      180.0

  # Calculates sine of current angle and returns it as `org.eolang.number`.
  # Please note, that `sin` is calculated from the angle in radians.
  [] > sin ?

  # Calculates cosine of current angle and returns it as `org.eolang.number`.
  # Please note, that `cos` is calculated from the angle in radians.
  [] > cos ?

  # Tangent of current angle.
  # Please note, that `tan` is calculated from the angle in radians.
  [] > tan
    cos > cosine!
    if. > @
      cosine.eq 0
      nan
      sin.div cosine

  # Cotangent of current angle.
  # Please note, that `ctan` is calculated from the angle in radians.
  [] > ctan
    sin > sine!
    if. > @
      sine.eq 0
      nan
      cos.div sine
