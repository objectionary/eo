# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.math.pi
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.math
+rt jvm org.eolang:eo-runtime:0.45.0
+rt node eo2js-runtime:0.0.0
+version 0.45.0

# The angle.
[value] > angle
  value > @
  # Converts this from radians to degrees
  angle > in-degrees
    div.
      ^.times 180.0
      pi
  # Converts this from degrees to radians
  angle > in-radians
    div.
      $.times pi
      180.0

  # Sine of current angle.
  # Please note, that `sin` is calculated from the angle in radians.
  [] > sin /number

  # Cosine of current angle.
  # Please note, that `cos` is calculated from the angle in radians.
  [] > cos /number

  # Tangent of current angle.
  # Please note, that `tan` is calculated from the angle in radians.
  [] > tan
    ^.cos > cosine!
    if. > @
      cosine.eq 0
      nan
      ^.sin.div cosine

  # Cotangent of current angle.
  # Please note, that `ctan` is calculated from the angle in radians.
  [] > ctan
    ^.sin > sine!
    if. > @
      sine.eq 0
      nan
      ^.cos.div sine

