+alias org.eolang.structs.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.math
+version 0.58.1
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Sequence of numbers.
# Here `sequence` must be a `tuple` or any `tuple` decorator of `number` objects.
[sequence] > numbers
  sequence > @

  # Find max element in the numbers sequence.
  [] > max
    if. > @
      lst.is-empty
      error "Can't get the max number from an empty sequence"
      reduced.
        lst
        negative-infinity
        [max item]
          if. > @
            item.as-number.gt max
            item
            max
    list sequence > lst

  # Find min element in the numbers sequence.
  [] > min
    if. > @
      lst.is-empty
      error "Can't get the min number from an empty sequence"
      reduced.
        lst
        positive-infinity
        [min item]
          if. > @
            min.gt item.as-number
            item
            min
    list sequence > lst

  # This unit test is supposed to check the functionality of the corresponding object.
  (numbers *).max > [] +> throws-on-taking-max-from-empty-sequence-of-numbers

  # This unit test is supposed to check the functionality of the corresponding object.
  (numbers *).min > [] +> throws-on-taking-min-from-empty-sequence-of-numbers

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-max-of-one-item-array
    eq. > @
      (numbers (* 42)).max
      42

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-min-of-one-item-array
    eq. > @
      (numbers (* 42)).min
      42

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-max-of-array-is-first
    eq. > @
      (numbers (* 25 12 -2)).max
      25

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-max-of-array-is-in-the-center
    eq. > @
      (numbers (* 12 25 -2)).max
      25

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-max-of-array-is-last
    eq. > @
      (numbers (* 12 -2 25)).max
      25

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-min-of-array-is-first
    eq. > @
      (numbers (* -2 25 12)).min
      -2

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-min-of-array-is-in-the-center
    eq. > @
      (numbers (* 12 -2 25)).min
      -2

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-min-of-array-is-last
    eq. > @
      (numbers (* 12 25 -2)).min
      -2
