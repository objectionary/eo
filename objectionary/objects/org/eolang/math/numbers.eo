+alias org.eolang.structs.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.math
+version 0.56.2
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint unit-test-missing

# Sequence of numbers.
# Here `sequence` must be a `tuple` or any `tuple` decorator of `number` objects.
[sequence] > numbers
  sequence > @

  # Find max element in the numbers sequence.
  [] > max
    if. > @
      lst.is-empty
      error "Can't get max number from empty sequence"
      reduced.
        lst
        negative-infinity
        [max item]
          if. > @
            item.as-number.gt max
            item
            max
    list sequence > lst

  # Find min element in the numbers sequence.
  [] > min
    if. > @
      lst.is-empty
      error "Can't get min number from empty sequence"
      reduced.
        lst
        positive-infinity
        [min item]
          if. > @
            min.gt item.as-number
            item
            min
    list sequence > lst
