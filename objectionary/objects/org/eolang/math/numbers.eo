+alias org.eolang.structs.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.math
+version 0.53.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Sequence of numbers.
# Here `sequence` must be a `tuple` or any `tuple` decorator of `number` objects.
[sequence] > numbers
  sequence > @

  # Find max element in the numbers sequence.
  [] > max
    list sequence > lst
    if. > @
      lst.is-empty
      error "Can't get max number from empty sequence"
      reduced.
        lst
        negative-infinity
        [max item]
          if. > @
            item.as-number.gt max
            item
            max

  # Find min element in the numbers sequence.
  [] > min
    list sequence > lst
    if. > @
      lst.is-empty
      error "Can't get min number from empty sequence"
      reduced.
        lst
        positive-infinity
        [min item]
          if. > @
            min.gt item.as-number
            item
            min
