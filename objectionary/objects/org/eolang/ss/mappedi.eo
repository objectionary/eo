+alias org.eolang.ss.list
+alias org.eolang.ss.reducedi
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.ss
+version 0.59.5
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Map `lst` with index. Here "func" must be an abstract
# object with two free attributes. The first
# one for the element of the collection, the second one
# for the index.
[lst func] > mappedi
  list > @
    reducedi
      lst
      *
      [accum item idx] >>
        accum.with > @
          func item idx

  # Tests that mapping with index access transforms elements correctly.
  [] +> tests-list-mappedi-should-work
    eq. > @
      mappedi
        list
          * 1 2 3 4
        i.times x > [x i]
      * 0 2 6 12
