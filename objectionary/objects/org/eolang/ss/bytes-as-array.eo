+alias org.eolang.ss.bytes-as-array
+alias org.eolang.ss.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.ss
+version 0.59.1
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Represents sequence of bytes as array.
# Here `bts` is `bytes` objects, the return value is `tuple` where
# each element is one `byte`.
[bts] > bytes-as-array
  slice-byte * 0 > @
  bts > dataized-bts!
  dataized-bts.size > bytes-size!

  [tup index] > slice-byte
    if. > @
      index.lt bytes-size
      slice-byte
        tup.with (bts.slice index 1)
        (index.plus 1).as-number
      tup

  # Tests that bytes are correctly converted to an array.
  [] +> tests-converts-bytes-to-array
    eq. > @
      list
        bytes-as-array
          20-1F-EE-B5-FF
      * 20- 1F- EE- B5- FF-

  # Tests that a single byte is correctly converted to an array.
  [] +> tests-single-byte-to-array
    eq. > @
      list
        bytes-as-array
          1F-
      * 1F-

  # Tests that zero bytes are correctly converted to an empty array.
  [] +> tests-zero-bytes-to-array
    eq. > @
      list
        bytes-as-array
          --
      *
