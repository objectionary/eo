+alias org.eolang.ss.list
+alias org.eolang.ss.reducedi
+alias org.eolang.tt.sprintf
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.ss
+version 0.59.5
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Reduce `lst` from "start" using the function "func".
# Here "func" must be an abstract object with two free attributes.
# The first one for the accumulator, the second one for the element
# of the collection.
[lst start func] > reduced
  reducedi > @
    lst
    start
    func accum item > [accum item idx] >>

  # Tests that reducing a list without index access works correctly.
  [] +> tests-list-reduce-without-index
    eq. > @
      reduced
        list
          * 1 2 3 4
        -1
        a.times x > [a x]
      -24

  # Tests that reducing a list to create a formatted string works correctly.
  [] +> tests-list-reduced-printing
    eq. > @
      reduced
        list
          * 0 1
        ""
        [acc item]
          acc.concat > @
            sprintf
              "%d"
              * item
      "01"
