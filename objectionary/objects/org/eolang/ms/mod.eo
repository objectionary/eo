+alias org.eolang.ms.real
+alias org.eolang.ms.abs
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.ms
+version 0.59.5
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Calculate MOD.
# An operation that finds the remainder after dividing `x` by `y`.
[x y] > mod
  if. > @
    divisor.eq 0
    error "Can't calculate mod by zero"
    if.
      dividend.gt 0
      abs-mod
      abs-mod.neg
  number x.as-bytes > dividend
  number y.as-bytes > divisor

  [] > abs-mod
    minus. > @
      dividend-abs
      divisor-abs.times
        floor.
          dividend-abs.div divisor-abs
    abs dividend > dividend-abs
    abs divisor > divisor-abs

  # Tests that modulo operation works correctly for 1 mod 2.
  [] +> tests-mod-n1-n2
    eq. +> @
      mod 1 2
      1

  # Tests that modulo operation works correctly for 0 mod 5.
  [] +> tests-mod-n0-n5
    eq. +> @
      mod 0 5
      0

  # Tests that modulo operation works correctly for 0 mod -15.
  [] +> tests-mod-n0-n15-neg
    eq. +> @
      mod 0 -15
      0

  # Tests that modulo operation works correctly for -1 mod 7.
  [] +> tests-mod-n1-neg-n7
    eq. +> @
      mod -1 7
      -1

  # Tests that modulo operation works correctly for 16 mod -200.
  [] +> tests-mod-n16-n200-neg
    eq. +> @
      mod 16 -200
      16

  # Tests mathematical equality A = ((A div B) * B) + (A mod B).
  # Checks mathematical equality
  # A = ((A div B) * B) + (A mod B).
  [] +> tests-div-mod-compatibility
    eq. +> @
      plus.
        mod dividend divisor
        times.
          divisor
          (dividend.div divisor).floor
      dividend
    -13 > dividend
    5 > divisor

  # Tests modulo operation when dividend is less than divisor.
  # Checks modulo: dividend < divisor.
  [] +> tests-mod-dividend-less-than-divisor
    eq. +> @
      mod -1 5
      -1

  # Tests modulo operation when divisor is 1.
  # Checks modulo by 1.
  [] +> tests-mod-dividend-by-one
    eq. +> @
      mod 133 1
      0
