+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.56.5
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# The object allows to choose right options according to cases conditions.
# Parameter cases is the array of two dimensional array, which
# consist of condition bool value and expected value, if this
# condition is true. For example:
#
# ```
# switch
#   *
#     *
#       true
#       "this value will be returned"
#     *
#       false
#       "this value will be skipped"
# ```
#
# This object returns value of only first truly statement.
[cases] > switch
  if. > @
    cases.length.eq 0
    error "Switch cases are empty"
    if.
      true.eq found.match
      found.value
      true
  (rec-case cases).self > found

  [tup] > rec-case
    if. > @
      and.
        tup.length.gt 1
        previous.match
      previous
      [] >>
        $ > self
        tup.value.prev.value > match!
        tup.value.value > value
    (rec-case tup.prev).self > previous

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-switch-simple-case
    eq. > @
      switch
        *
          *
            false
            "1"
          *
            true
            "2"
      "2"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-switch-strings-case
    eq. > @
      switch
        *
          *
            password.eq "swordfish"
            "password is correct!"
          *
            password.eq ""
            "empty password is not allowed"
          *
            false
            "password is wrong"
      "password is correct!"
    "swordfish" > password

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-switch-with-several-true-cases
    eq. > @
      switch
        *
          *
            true
            "TRUE1"
          *
            false
            "FALSE"
          *
            true
            "TRUE2"
      "TRUE1"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-switch-with-all-false-cases
    switch > @
      *
        *
          false
          "false1"
        *
          false
          "false2"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> throws-on-empty-switch
    switch * > @

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-switch-complex-case
    eq. > @
      switch
        *
          *
            [] >>
              c1 > @
            22.0
          *
            [] >>
              c2 > @
            0.0
          *
            [] >>
              c3 > @
            []
              "true case" > @
      "true case"
    [] > c1
      false > @
    [] > c2
      "1".eq "2" > @
    [] > c3
      true > @
