+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.52.0

# The object allows to choose right options according to cases conditions.
# Parameter cases is the array of two dimensional array, which
# consist of condition bool value and expected value, if this
# condition is true. For example:
#
# ```
# switch
#   *
#     *
#       true
#       "this value will be returned"
#     *
#       false
#       "this value will be skipped"
# ```
#
# This object returns value of only first truly statement.
[cases] > switch
  (rec-case cases).self > found
  if. > @
    cases.length.eq 0
    error "Switch cases are empty"
    if.
      true.eq found.match
      found.value
      true

  [tup] > rec-case
    (rec-case tup.prev).self > previous
    if. > @
      and.
        tup.length.gt 1
        previous.match
      previous
      [] >>
        $ > self
        tup.value.prev.value > match!
        tup.value.value > value
