# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.46.0

# Tuple.
[head tail] > tuple
  # Empty tuple.
  [] > empty
    0 > length

    # Take one element from the tuple, at the given position.
    error "Can't get an object from the empty tuple" > [i] > at

    # Create a new tuple with this element added to the end of it.
    tuple ^ x > [x] > with

  # Obtain the length of the tuple.
  [] > length
    head.length.plus 1 > len!
    number len > @

  # Take one element from the tuple, at the given position.
  [i] > at
    ^.length > len
    i > idx!
    if. > index!
      0.gt idx
      len.plus idx
      idx
    if. > @
      or.
        0.gt index
        len.lte index
      error "Given index is out of tuple bounds"
      at-fast ^ len

    [tup len] > at-fast
      if. > @
        (len.plus -1).gt index
        ^.at-fast
          tup.head
          len.plus -1
        tup.tail

  # Create a new tuple with this element added to the end of it.
  tuple ^ x > [x] > with
