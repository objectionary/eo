+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.56.2
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Tuple.
# An ordered, immutable collection of elements.
[prev value length] > tuple
  $ > as-tuple

  # Empty tuple.
  # A tuple with no elements. When dataized, it represents an immutable, empty collection.
  [] > empty
    $ > empty
    $ > as-tuple
    0 > length
    error "Can't get prev from the empty tuple" > prev
    error "Can't get value from the empty tuple" > value

    # Take one element from the tuple, at the given position.
    error "Can't get an object from the empty tuple" > [i] > at

    # Create a new tuple with this element added to the end of it.
    [x] > with
      as-tuple. > @
        tuple
          ^
          x
          (length.plus 1).as-number

  # Take one element from the tuple, at the given position.
  [i] > at
    if. > @
      or.
        0.gt index
        length.lte index
      error "Given index is out of tuple bounds"
      at-fast ^
    i > idx!
    if. > index!
      0.gt idx
      length.plus idx
      idx

    [tup] > at-fast
      if. > @
        (tup.length.plus -1).eq index
        tup.value
        at-fast tup.prev

  # Create a new tuple with this element added to the end of it.
  [x] > with
    as-tuple. > @
      tuple
        ^
        x
        (length.plus 1).as-number
