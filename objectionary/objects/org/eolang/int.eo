# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.38.3
+version 0.38.3

# The `int` object is an abstraction of a 64-bit signed integer that
# internally is a chain of eight bytes.
[as-bytes] > int
  as-bytes > @

  # Tests that $ = x.
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that $ < x.
  [x] > lt
    0.gt > @
      ^.minus
        int
          x > value!

  # Tests that $ <= x.
  [x] > lte
    not. > @
      ^.gt
        x > value!

  # Tests that $ > x.
  [x] > gt /bool

  # Tests that $ >= x.
  [x] > gte
    not. > @
      ^.lt
        x > value!

  # Change the sign of the number.
  ^.times -1 > [] > neg

  # Add to the current one.
  [x] > plus /int

  # Subtract from the current one.
  ^.plus x.neg > [x] > minus

  # Multiplication this one by another int.
  [x] > times /int

  # Divide this one by another int.
  [x] > div /int
