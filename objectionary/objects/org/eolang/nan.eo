+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.53.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# The `not a number` object is an abstraction
# for representing undefined or unrepresentable numerical results.
[] > nan
  number 7F-F8-00-00-00-00-00-00 > @
  $ > floor
  $ > neg
  true > is-nan
  false > is-finite
  false > is-integer
  error "Can't convert NaN to i64" > as-i64

  # Tests that $ value is equal to x.
  false > [x] > eq

  # Tests that the value $ less than x.
  false > [x] > lt

  # Tests that $ less or equal than x.
  false > [x] > lte

  # Tests that $ value greater than x.
  false > [x] > gt

  # Tests that $ greater or equal than x.
  false > [x] > gte

  # Returns the multiplication of $ and x.
  ^ > [x] > times

  # Returns the result of the sum of $ and x.
  ^ > [x] > plus

  # Returns the difference between $ and x.
  ^ > [x] > minus

  # Quotient of the division of $ by x.
  ^ > [x] > div
