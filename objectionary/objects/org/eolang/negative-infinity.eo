+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.56.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Negative infinity.
# A special floating-point value representing an unbounded quantity less than all real numbers.
# When dataized, it signifies an unbounded lower limit or an unreachable maximum value.
[] > negative-infinity
  number FF-F0-00-00-00-00-00-00 > @
  $ > floor
  positive-infinity > neg
  false > is-nan
  false > is-finite
  false > is-integer
  error "Can't convert negative infinity to i64" > as-i64

  # Tests that the value $ is equal to x.
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that the value $ less than x.
  [x] > lt
    not. > @
      or.
        (number value).is-nan
        eq value
    x > value!

  # Tests that the value $ less or equal than x.
  [x] > lte
    not. > @
      (number value).is-nan
    x > value!

  # Tests that the value $ greater than x.
  false > [x] > gt

  # Tests that the value $ greater or equal than x.
  eq x > [x] > gte

  # Returns the result of the multiplication of $ and x.
  [x] > times
    if. > @
      or.
        num.is-nan
        num.eq 0
      nan
      if.
        num.gt 0
        ^
        positive-infinity
    x > value!
    number value > num

  # Returns the result of the sum of $ and x.
  [x] > plus
    if. > @
      or.
        (number value).is-nan
        value.eq positive-infinity
      nan
      ^
    x > value!

  # Difference between the values of $ and x.
  [x] > minus
    if. > @
      or.
        (number value).is-nan
        value.eq ^
      nan
      ^
    x > value!

  # Quotient of the division of $ by x.
  [x] > div
    if. > @
      or.
        num.is-nan
        num.is-finite.not
      nan
      if.
        or.
          value.eq -0.as-bytes
          0.gt value
        positive-infinity
        ^
    x > value!
    number value > num