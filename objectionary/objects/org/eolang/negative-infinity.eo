+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.55.1
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Negative infinity.
# A special floating-point value representing an unbounded quantity less than all real numbers.
# When dataized, it signifies an unbounded lower limit or an unreachable maximum value.
[] > negative-infinity
  number FF-F0-00-00-00-00-00-00 > @
  $ > floor
  positive-infinity > neg
  false > is-nan
  false > is-finite
  false > is-integer
  error "Can't convert negative infinity to i64" > as-i64

  # Tests that the value $ is equal to x.
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that the value $ less than x.
  [x] > lt
    x > value!
    not. > @
      or.
        (number value).is-nan
        eq value

  # Tests that the value $ less or equal than x.
  [x] > lte
    x > value!
    not. > @
      (number value).is-nan

  # Tests that the value $ greater than x.
  false > [x] > gt

  # Tests that the value $ greater or equal than x.
  eq x > [x] > gte

  # Returns the result of the multiplication of $ and x.
  [x] > times
    x > value!
    number value > num
    if. > @
      or.
        num.is-nan
        num.eq 0
      nan
      if.
        num.gt 0
        ^
        positive-infinity

  # Returns the result of the sum of $ and x.
  [x] > plus
    x > value!
    if. > @
      or.
        (number value).is-nan
        value.eq positive-infinity
      nan
      ^

  # Difference between the values of $ and x.
  [x] > minus
    x > value!
    if. > @
      or.
        (number value).is-nan
        value.eq ^
      nan
      ^

  # Quotient of the division of $ by x.
  [x] > div
    x > value!
    number value > num
    if. > @
      or.
        num.is-nan
        num.is-finite.not
      nan
      if.
        or.
          value.eq -0.as-bytes
          0.gt value
        positive-infinity
        ^
