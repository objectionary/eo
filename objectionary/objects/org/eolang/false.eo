+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+version 0.58.2
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:12

# The object is a FALSE boolean state.
[] > false
  00- > @
  true > not

  # Control structure that allows for conditional execution of code.
  # Since $ is FALSE - behaves as `right` object.
  right > [left right] > if

  # And.
  # A logical operation that returns true only if all given conditions are true.
  ^ > [x] > and

  # Or.
  # A logical operation that returns true if at least one of the given conditions is true.
  01-.eq x > [x] > or

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-true-not-is-false
    eq. > @
      true.not
      false

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-compares-bool-to-string
    and. > @
      true.eq "\001"
      false.eq "\000"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-true-and-false-is-false
    not. > @
      and.
        true
        false

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-forks-on-false-condition
    eq. > @
      if.
        5.eq 8
        123
        42
      42
