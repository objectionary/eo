+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.io
+version 0.52.0

# Tee input is an input that reads from provided `input`,
# writes to provided `output` and behaves as provided `input`.
# To copy bytes input to memory you can do:
# ```
# malloc.of > copied
#   5
#   [m]
#     read. > @
#       tee-input
#         bytes-as-input 01-02-03-04-05
#         malloc-as-output m
#       5
# ```
# After dataization `copied` is equal to `01-02-03-04-05` which are read
# from memory.
[input output] > tee-input
  # Read `size` amount of bytes from `input`.
  # Returns new instance of `input-block` with set
  # `input` ready to be read, `output` ready to be written and bytes `buffer`.
  [size] > read
    ((input-block input output --).read size).self > @

    # Tee-input block.
    #
    # Attention! The object is for internal usage only, please don't use the object
    # programmatically outside of `tee-input` object.
    #
    # Here `input` is an input ready to be read,
    # `output` is an output ready to be written,
    # `buffer` is a sequence of bytes that got from previous reading.
    [input output buffer] > input-block
      $ > self
      buffer > @

      # Read `size` amount of bytes from `input`.
      # Returns new instance of `input-block` with set
      # `input` ready to be read, `output` ready to be written and bytes `buffer`.
      [size] > read
        (input.read size).read.^ > read-bytes
        (output.write read-bytes).write.^ > written-bytes
        self. > @
          seq *
            written-bytes
            input-block
              read-bytes
              written-bytes
              read-bytes.as-bytes
