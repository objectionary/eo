+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.io
+version 0.56.9
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Makes an output from allocated block in memory.
# Here `allocated` is `malloc.of.allocated` object.
#
# Imagine you want to read bytes from `console` and save them
# to block in memory.
# Here's how you can do it:
# ```
# malloc.of > result
#   10
#   [mem]
#     console.read 10 > input
#     malloc-as-output mem > output
#     output.write input > @
# ```
#
# First, you read 10 bytes from `console`.
# Second, you create an output from allocated block in memory.
# Third, you write read bytes to output
# After the dataization the object `result` contains read bytes.
[allocated] > malloc-as-output
  # Writes bytes contained in `buffer` to operation system console.
  # Returns new instance of `output-block` ready to write again.
  [buffer] > write
    self. > @
      (output-block 0).write buffer

    # Malloc output block.
    #
    # Attention! The object is for internal usage only, please don't use the object
    # programmatically outside of `malloc-as-output` object.
    #
    # Here `offset` is the offset to write to allocated block in memory with.
    [offset] > output-block
      true > @
      $ > self

      # Writes bytes contained in `buffer` to operation system console.
      # Returns new instance of `output-block` ready to write again.
      [buffer] > write
        self. > @
          seq *
            allocated.write offset buffer
            output-block
              offset.plus buffer.size

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-makes-an-output-from-malloc-and-writes
    eq. > @
      malloc.of
        10
        [mem]
          seq > @
            *
              o2.write 08-09-A0
              mem
          malloc-as-output mem > out
          out.write 01-02-03 > o1
          o1.write 04-05-06-07 > o2
      01-02-03-04-05-06-07-08-09-A0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> throws-on-writing-more-than-allocated
    malloc.of > @
      2
      [m]
        (malloc-as-output m).write 01-02-03 > @
