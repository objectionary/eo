+alias org.eolang.io.bytes-as-input
+alias org.eolang.io.malloc-as-output
+alias org.eolang.io.tee-input
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.io
+version 0.59.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Reads all the bytes from provided `input` and returns its length.
[input] > input-length
  rec-read input 0 > @

  [input length] > rec-read
    if. > @
      read-bytes.size.eq 0
      length
      rec-read
        read-bytes
        length.plus read-bytes.size
    (input.read 4096).read.^ > read-bytes

  # Tests that input-length reads all bytes from input and returns the total length.
  [] +> tests-reads-all-bytes-and-returns-length
    eq. > @
      input-length
        bytes-as-input
          01-02-03-04-05-06-07-08-09-10
      10

  # Tests that input-length copies all bytes from tee-input to output while returning the length.
  [] +> tests-copies-all-bytes-to-output-and-returns-length
    eq. > @
      malloc.of
        10
        [m]
          seq > @
            *
              input-length
                tee-input
                  bytes-as-input 01-02-03-04-05-06-07-08-09-10
                  malloc-as-output m
              m
      01-02-03-04-05-06-07-08-09-10
