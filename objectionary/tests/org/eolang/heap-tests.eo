# The MIT License (MIT)
#
# Copyright (c) 2016-2023 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.hamcrest.assert-that
+alias org.eolang.heap
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+version 0.31.0

[] > allocates-and-uses
  "Hello, друг!" > txt
  txt.as-bytes > bytes
  heap 1024 > h
  h.pointer 0 1 > p
  p.block > data
    bytes.size
    [b] (b.as-string > @)
  assert-that > @
    seq
      data.write bytes
      data
    $.equal-to txt

[] > malloc-returns-different-pointers
  heap 1024 > h!
  h.malloc 64 > p1!
  h.malloc 32 > p2!
  assert-that > @
    eq.
      seq
        QQ.io.stdout
          QQ.txt.sprintf
            "p1 = %d\np2 = %d\n"
            p1
            p2
        p1
      p2
    $.equal-to FALSE

[] > mallocs-do-not-overlap
  heap 1024 > h!
  h.malloc 64 > p1!
  h.malloc 32 > p2!
  assert-that > @
    or.
      p2.gte
        p1.plus 64
      p2.lte
        p1.minus 32
    $.equal-to TRUE

[] > malloc-return-error
  heap 2 > h!
  assert-that > @
    try
      []
        h.malloc > @
          10
      [e]
        e > @
      nop
    $.equal-to
      "Allocation failed: bad alloc (not enough memory in the heap)"

[] > write-and-read-without-error
  heap 1024 > h
  h.pointer 0 8 > p
  [] > mem
    p.block 8 > @
  assert-that > @
    mem.write 01-02-03
    $.equal-to TRUE

[] > increments-pointer
  heap 1024 > h
  h.pointer 0 2 > p1
  p1.add 2 > p2
  p2.sub 1 > p3
  assert-that > @
    and.
      (p1.block 6).write ("HELLO!".as-bytes)
      eq.
        p2.block
          2
          [b] (b.as-string > @)
        "O!"
      eq.
        p3.block
          2
          [b] (b.as-string > @)
        "LL"
    $.equal-to TRUE
