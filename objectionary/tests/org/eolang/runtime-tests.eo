# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+unlint abstract-decoratee
+version 0.48.2

# This unit test is supposed to check the functionality of the corresponding object.
[] > understands-this-correctly
  [x] > a
    $.x > @
  eq. > @
    a 42
    42

# This unit test is supposed to check the functionality of the corresponding object.
[] > takes-parent-object
  [x] > a
    [] > take
      ^.x > @
    take > @
  eq. > @
    a 42
    42

# This unit test is supposed to check the functionality of the corresponding object.
[] > makes-object-a-constant
  [] > foo
    times. > @
      50
      50
  foo.@ > f!
  eq. > @
    f
    f

# This unit test is supposed to check the functionality of the corresponding object.
[] > takes-parent-through-attribute
  42 > x
  $ > this
  [] > @
    [] > @
      [] > @
        eq. > @
          this.x
          42

# This unit test is supposed to check the functionality of the corresponding object.
[] > throws-when-applies-to-closed-object
  [x] > a
    x > @
  a false > closed
  closed true > @

# This unit test is supposed to check the functionality of the corresponding object.
[] > makes-deep-object-recursively
  eq. > @
    x 5
    0
  [i] > x
    if. > @
      i.lt 0
      0
      x
        i.minus 1

# This unit test is supposed to check the functionality of the corresponding object.
[] > calculates-only-once
  eq. > @
    malloc.for
      0
      [m] > x
        [] > a
          seq > @
            *
              ^.m.put (^.m.as-number.plus 1)
              42
        seq > @
          *
            a.neg.neg.neg.neg.eq 42
            m
    1

# This unit test is supposed to check the functionality of the corresponding object.
[] > recursion-without-arguments
  [n] > func
    if. > @
      n.as-number.gt 0
      seq
        *
          n.put (n.as-number.minus 1)
          ^.func n
      n
  eq. > @
    malloc.for
      4
      [m] >>
        ^.func m > @
    0

# This unit test is supposed to check the functionality of the corresponding object.
[] > unescapes-slashes
  eq. > @
    "x\\b\\f\\u\\r\\t\\n\\'"
    78-5C-62-5C-66-5C-75-5C-72-5C-74-5C-6E-5C-27

# This unit test is supposed to check the functionality of the corresponding object.
[] > unescapes-symbols
  eq. > @
    "\b\f\n\r\t\u27E6"
    08-0C-0A-0D-09-E2-9F-A6

# This unit test is supposed to check the functionality of the corresponding object.
[] > compiles-correctly-with-long-duplicate-names
  [] > long-object-name
    [] > long-object-name
      [] > long-object-name
        [] > long-object-name
          [] > long-object-name
            42 > x
  true > @

# This unit test is supposed to check the functionality of the corresponding object.
[] > named-inner-abstract-object
  seq > @
    *
      [] > a
        true > @

# This unit test is supposed to check the functionality of the corresponding object.
[] > app-that-calls-func
  [] > app
    [args] > f
      1 > a
      2 > @
    f > @
      * 1 2 3
  app > output
  eq. > @
    output
    2

# This unit test is supposed to check the functionality of the corresponding object.
[] > directly-accesses-objects-from-root
  eq. > @
    Q.org.eolang.malloc.of
      8
      [m]
        seq > @
          *
            m.put 42
            m.put
              m.as-number.minus 2
    40

# This unit test is supposed to check the functionality of the corresponding object.
[] > directly-accesses-objects-from-standard-root
  eq. > @
    QQ.malloc.of
      8
      [m]
        seq > @
          *
            m.put 42
            m.put
              m.as-number.minus 2
    40

# This unit test is supposed to check the functionality of the corresponding object.
[] > standard-root-and-root
  QQ.sys.os > stand-root
  Q.org.eolang.sys.os > root
  eq. > @
    root
    stand-root

# This unit test is supposed to check the functionality of the corresponding object.
[] > extract-attribute-from-decoratee
  [foo] > return
  [] > a
    ^.return > @
      plus.
        42
        1
  eq. > @
    a.foo
    43

# This unit test is supposed to check the functionality of the corresponding object.
[] > constant-defends-against-side-effects
  [x] > inc
    seq > @
      *
        x.put
          x.as-number.plus 1
        x.as-number
  eq. > @
    malloc.for
      7
      [m] >>
        number > num
          ^.inc m > n!
        m.put > @
          times.
            num
            num
    64

# This unit test is supposed to check the functionality of the corresponding object.
[] > parent-in-vertical-notation
  5 > m
  [] > value
    [] > @
      m. > @
        ^.
          ^
  eq. > @
    value
    5

# This unit test is supposed to check the functionality of the corresponding object.
[] > parent-in-horizontal-notation
  5 > m
  [] > value
    [] > @
      ^.^.m > @
  eq. > @
    value
    5

# This unit test is supposed to check the functionality of the corresponding object.
[] > phi-in-vertical-notation
  [] > value
    [] > @
      100 > @
  eq. > @
    @.
      value
    100

# This unit test is supposed to check the functionality of the corresponding object.
[] > phi-in-horizontal-notation
  [] > value
    [] > @
      100 > @
  eq. > @
    value.@
    100

# This unit test is supposed to check the functionality of the corresponding object.
[] > right-way-to-use-hierarchy
  # Bool mock
  [value] > pybool
    value > @
  [value] > pyint
    [x] > eq
      ^.^.pybool (^.value.eq x.value) > @
    [x] > add
      ^.^.pyint (^.value.plus x.value) > @
  ((pyint 1).add (pyint 3)).eq (pyint 4) > @

# This unit test is supposed to check the functionality of the corresponding object.
[] > check-triple-quotes
  eq. > @
    """
    Hello

    Hello
    """
    "Hello\n\nHello"

# This unit test is supposed to check the functionality of the corresponding object.
[] > correctly-handles-same-name-attrs-simplified
  [first second] > calc
    plus. > @
      first
      second
  eq. > @
    calc
      []
        build 1 > @
        [x] > build
          x > @
      []
        build 2 > @
        [y] > build
          y > @
    3

# This unit test is supposed to check the functionality of the corresponding object.
[] > correctly-handles-same-name-attrs
  [f s] > calc
    plus. > @
      f.next
      s.next
  eq. > @
    calc
      []
        [x] > build
          x.plus 1 > @
          ^.build > next
            plus.
              x
              1
        build 1 > @
      []
        [y] > build
          y.plus 2 > @
          ^.build > next
            plus.
              y
              2
        build 2 > @
    9

# Nesting blah test.
[] > nesting-blah-test
  blah0 > @
  [] > blah0
    blah1 > @
    [] > blah1
      blah2 > @
      [] > blah2
        blah3 > @
        [] > blah3
          blah4 > @
          [] > blah4
            blah5 > @
            [] > blah5
              blah6 > @
              [] > blah6
                blah7 > @
                [] > blah7
                  blah8 > @
                  [] > blah8
                    blah9 > @
                    [] > blah9
                      blah10 > @
                      [] > blah10
                        blah11 > @
                        [] > blah11
                          blah12 > @
                          [] > blah12
                            blah13 > @
                            [] > blah13
                              blah14 > @
                              [] > blah14
                                blah15 > @
                                [] > blah15
                                  blah16 > @
                                  [] > blah16
                                    blah17 > @
                                    [] > blah17
                                      blah18 > @
                                      [] > blah18
                                        blah19 > @
                                        [] > blah19
                                          blah20 > @
                                          [] > blah20
                                            blah21 > @
                                            [] > blah21
                                              blah22 > @
                                              [] > blah22
                                                blah23 > @
                                                [] > blah23
                                                  blah24 > @
                                                  [] > blah24
                                                    blah25 > @
                                                    [] > blah25
                                                      blah26 > @
                                                      [] > blah26
                                                        blah27 > @
                                                        [] > blah27
                                                          blah28 > @
                                                          [] > blah28
                                                            blah29 > @
                                                            [] > blah29
                                                              blah30 > @
                                                              [] > blah30
                                                                blah31 > @
                                                                [] > blah31
                                                                  blah32 > @
                                                                  [] > blah32
                                                                    blah33 > @
                                                                    [] > blah33
                                                                      blah34 > @
                                                                      [] > blah34
                                                                        blah35 > @
                                                                        [] > blah35
                                                                          blah36 > @
                                                                          [] > blah36
                                                                            blah37 > @
                                                                            [] > blah37
                                                                              blah38 > @
                                                                              [] > blah38
                                                                                blah39 > @
                                                                                [] > blah39
                                                                                  true > @
