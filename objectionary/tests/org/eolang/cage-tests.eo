# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+unlint abstract-decoratee
+version 0.43.0

# This unit test is supposed to check the functionality of the corresponding object.
[] > encages-into-cage
  # Object with free attribute.
  [z] > a
  (cage a).new > x
  seq > @
    *
      x.encage
        a 7
      x.z.eq 7

# This unit test is supposed to check the functionality of the corresponding object.
[] > checks-forma-and-fixes
  (cage 0).new > x
  (cage number.plus).new > sum
  seq > @
    *
      x.encage 42
      sum.encage
        x.plus 1
      x.encage 7
      sum.eq 43

# This unit test is supposed to check the functionality of the corresponding object.
[] > dataizes-encaged-object-lazily-second
  (cage 0).new > x
  (cage number.plus).new > sum
  seq > @
    *
      x.encage 42
      sum.encage
        1.plus x
      x.encage 7
      sum.eq 8

# This unit test is supposed to check the functionality of the corresponding object.
[] > stores-abstract-object-into-cage
  new. > c
    cage
      112 > []
  c.eq 112 > @

# This unit test is supposed to check the functionality of the corresponding object.
[] > multi-layer-volatility
  (cage a).new > ca
  [] > a
    [] > @
      [] > @
        [] > @
          malloc.for > @
            0
            ma.put (ma.as-number.plus 1) > [ma]
  (cage b).new > cb
  [] > b
    [] > @
      [] > @
        [] > @
          malloc.for > z
            0
            mb.put (mb.as-number.plus 1) > [mb]
  eq. > @
    seq
      *
        ca
        ca.as-number.plus 40
    seq
      *
        cb.z
        cb.z.as-number.plus 40

# This unit test is supposed to check the functionality of the corresponding object.
[] > infinite-loop-check
  [value] > pyint
    [y] > add
      ^.^.pyint (^.value.plus y.value) > @
  (cage pyint).new > x
  (cage x.add).new > tmp
  seq > @
    *
      x.encage
        pyint 0
      tmp.encage
        x.add (pyint 1)
      tmp.value.eq 1

# This unit test is supposed to check the functionality of the corresponding object.
[] > catches-encaging-objects-of-different-formas
  (cage number).new > cge
  seq > @
    *
      try
        cge.encage "Hello world"
        ^.cge.encage 10 > [e] >>
        true
      cge.eq 10

# Check that cage stack leads to error. Just prints the error
# message if passes.
[] > throws-on-cage-stack-overflow
  (cage number.plus).new > cge
  seq > @
    *
      cge.write
        0.plus cge
      cge
      false

# This unit test is supposed to check the functionality of the corresponding object.
[] > applies-after-retrieval
  new. > c
    cage
      []
        [x] > func
          2.plus x > @
  eq. > @
    c.func 5
    7
