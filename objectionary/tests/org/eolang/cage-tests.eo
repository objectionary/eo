# The MIT License (MIT)
#
# Copyright (c) 2016-2023 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.hamcrest.assert-that
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+version 0.31.0

[] > writes-into-cage
  [z] > a
  cage a > x
  assert-that > @
    seq
      x.write
        a 7
      x.z
    $.equal-to 7

[] > avoid-infinite-loop
  cage > x
    TRUE.if
  x' > copy
  assert-that > @
    seq
      x.write
        TRUE.if 1 0
      copy.<
      x.write
        TRUE.if copy 0
      x
    $.equal-to 1

[] > cage-nested-objects
  [x] > dummy
    x > @
  cage dummy > c
  c' > copy
  assert-that > @
    seq
      c.write (dummy 42)
      copy.<
      c.write (dummy copy)
      c.x.x
    $.equal-to 42

[] > dataizes-encaged-object-lazily-first
  cage 0 > x
  cage 0.plus > sum
  assert-that > @
    seq
      x.write 42
      sum.write
        x.plus 1
      x.write 7
      sum
    $.equal-to 8

[] > dataizes-encaged-object-lazily-second
  cage 0 > x
  cage 0.plus > sum
  assert-that > @
    seq
      x.write 42
      sum.write
        1.plus x
      x.write 7
      sum
    $.equal-to 8

[] > dataizes-encaged-object-lazily-third
  cage 0 > x
  cage 0 > y
  cage 0.plus > sum
  assert-that > @
    seq
      x.write 42
      y.write 7
      sum.write
        x.plus y
      y.write 13
      x.write 4
      sum
    $.equal-to 17

[] > stores-abstract-object-into-cage
  assert-that > @
    seq
      cage > c
        [x]
          112 > @
      c.@
        42
    $.equal-to 112

[] > multi-layer-volatility
  memory 0 > ma
  cage a > ca
  [] > a
    [] > @
      [] > @
        [] > @
          seq > @
            ma.write (ma.as-int.plus 1)
            ma.as-int
  memory 0 > mb
  cage b > cb
  [] > b
    [] > @
      [] > @
        [] > @
          seq > z
            mb.write (mb.as-int.plus 1)
            mb.as-int
  assert-that > @
    seq
      ca
      ca.plus 40
    $.equal-to
      seq
        cb.z
        cb.z.plus 40

[] > infinite-loop-check
  [value] > pyint
    [y] > add
      pyint (value.plus (y.value)) > @
  cage pyint > x
  cage > tmp
    x.add
  assert-that > @
    seq
      x.write
        pyint 0
      tmp.write
        x.add (pyint 1)
      tmp.value
    $.equal-to 1

[] > catches-writing-objects-of-different-types
  cage 5 > cge
  assert-that > @
    seq
      try
        cge.write "Hello world"
        [e]
          cge.write 10 > @
        nop
      cge.eq 10
    $.equal-to TRUE
