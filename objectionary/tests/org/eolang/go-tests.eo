# The MIT License (MIT)
#
# Copyright (c) 2016-2025 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+version 0.50.1
+unlint broken-ref

# This unit test is supposed to check the functionality of the corresponding object.
[] > goto-jumps-backwards
  eq. > @
    malloc.of
      8
      [i]
        seq > @
          *
            i.put 1
            go.to
              [g] >>
                seq > @
                  *
                    i.put (i.as-number.plus 1)
                    if.
                      i.as-number.lt 10
                      g.backward
                      true
            i
    10

# This unit test is supposed to check the functionality of the corresponding object.
[] > goto-jumps-forward
  [x] > div
    malloc.for > @
      0
      [r] >>
        go.to > @
          [g] >>
            seq > @
              *
                if.
                  x.eq 0
                  g.forward r
                  true
                r.put (42.div x)
  and. > @
    eq.
      div 7
      6
    eq.
      div 0
      0

# This unit test is supposed to check the functionality of the corresponding object.
[] > returns-from-method-body
  [a b] > max
    go.to > @
      [g] >>
        seq > @
          *
            if.
              a.gt b
              g.forward a
              true
            b
  eq. > @
    max 7 42
    42

# This unit test is supposed to check the functionality of the corresponding object.
[] > nested-goto
  eq. > @
    go.to
      [g1]
        seq > @
          *
            go.to
              [g2] >>
                g1.forward 42 > @
            7
    42
