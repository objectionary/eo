+alias org.eolang.structs.range
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang.structs
+version 0.56.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# This unit test is supposed to check the functionality of the corresponding object.
[] > range-tests
  # This unit test is supposed to check the functionality of the corresponding object.
  [] > tests-simple-range-from-one-to-ten
    eq. > @
      rng
      * 1 2 3 4 5 6 7 8 9
    range > rng
      []
        build 1 > @
        [i] > build
          i > @
          ^.build (1.plus @) > next
      10

  # This unit test is supposed to check the functionality of the corresponding object.
  [] > tests-simple-range-with-floats-from-one-to-five
    eq. > @
      rng
      * 1.0 1.5 2.0 2.5 3.0 3.5 4.0 4.5
    range > rng
      []
        x 1.0 > @
        [i] > x
          i > @
          ^.x (0.5.plus @) > next
      5.0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] > tests-range-with-out-of-bounds
    eq. > @
      rng
      * 1 6
    range > rng
      []
        b 1 > @
        [num] > b
          num > @
          ^.b (5.plus @) > next
      10

  # This unit test is supposed to check the functionality of the corresponding object.
  [] > tests-range-with-wrong-items-is-an-empty-array
    rng.is-empty > @
    range > rng
      []
        y 10 > @
        [num] > y
          num > @
          [] > next
            42 > nothing
      1
