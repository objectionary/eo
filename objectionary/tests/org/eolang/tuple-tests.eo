+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+version 0.55.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint sparse-decoration

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-makes-tuple-through-special-syntax
  eq. > @
    (* 1 2).length
    2

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-gets-lengths-of-empty-tuple-through-special-syntax
  eq. > @
    *.length
    0

# Check that an empty tuples .length equals to zero.
[] > tests-empty-tuple-length
  [elements] > arr
  eq. > @
    (arr *).elements.length
    0

# Check that tuple.length works properly for non-empty tuples.
[] > tests-non-empty-tuple-length-test
  [elements] > arr
  eq. > @
    arr
      * "a" "b" "c" "d" "e"
    .elements
    .length
    5

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-as-a-bound-attribute-size-0
  * > anArray
  eq. > @
    anArray.length
    0

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-as-a-bound-attribute-size-1
  * > anArray
    100
  eq. > @
    anArray.at 0
    100

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-as-a-bound-attribute-size-2
  * > arr
    1
    2
  and. > @
    eq.
      arr.at 0
      1
    eq.
      arr.at 1
      2

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-with
  with. > arr
    * 1 2 3
    "with"
  and. > @
    and.
      and.
        eq.
          arr.at 0
          1
        eq.
          arr.at 1
          2
      eq.
        arr.at 2
        3
    eq.
      arr.at 3
      "with"

# This unit test is supposed to check the functionality of the corresponding object.
[] > throws-on-wrong-tuple-at
  at. > @
    * 1 2 3 4
    20

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-fluent-with
  ((*.with 1).with 2).with 3 > arr
  and. > @
    and.
      eq.
        arr.at 0
        1
      eq.
        arr.at 1
        2
    eq.
      arr.at 2
      3

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-fluent-with-indented
  *
  .with 1
  .with 2
  .with 3 > arr
  and. > @
    and.
      eq.
        arr.at 0
        1
      eq.
        arr.at 1
        2
    eq.
      arr.at 2
      3

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-gets-lengths-of-empty-tuple
  tuple.empty > a
  eq. > @
    a.length
    0

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-gets-lengths-of-empty-tuple-without-additional-obj
  eq. > @
    tuple.empty.length
    0

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-creates-empty-tuple-with-number
  tuple.empty.with 3 > a
  eq. > @
    a.at 0
    3

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-at-fast-with-first-element
  * 100 101 102 > arr
  eq. > @
    (arr.at 0).at-fast arr
    100

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-at-fast-with-last-element
  * 100 101 102 > arr
  eq. > @
    (arr.at 2).at-fast arr
    102

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-empty-fluent-with-indented-keyword
  tuple
  .empty
  .with 1
  .with 2
  .with 3 > arr
  and. > @
    and.
      eq.
        arr.at 0
        1
      eq.
        arr.at 1
        2
    eq.
      arr.at 2
      3

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-with-negative-index-gets-last
  * 0 1 2 3 4 > arr
  eq. > @
    arr.at -1
    4

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-tuple-with-negative-index-gets-first
  * 0 1 2 3 4 > arr
  eq. > @
    arr.at -5
    0

# This unit test is supposed to check the functionality of the corresponding object.
[] > throws-on-out-of-tuple-bounds-with-negative-index
  * 0 1 2 3 4 > arr
  arr.at -6 > @
