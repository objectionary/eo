+alias org.eolang.sys.os
+alias org.eolang.sys.posix
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang.sys
+version 0.54.1
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint sparse-decoration

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-invokes-getpid-correctly
  or. > @
    os.is-windows
    gt.
      code.
        posix
          "getpid"
          tuple.empty
      0

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-opens-posix-tcp-socket
  code. > sd
    posix
      "socket"
      * posix.af-inet posix.sock-stream posix.ipproto-tcp
  or. > @
    os.is-windows
    try
      sd.gt 0
      ex > [ex]
      code.
        posix
          "close"
          * sd

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-closes-posix-tcp-socket
  code. > sd
    posix
      "socket"
      * posix.af-inet posix.sock-stream posix.ipproto-tcp
  or. > @
    os.is-windows
    seq
      *
        sd
        not.
          eq.
            code.
              posix
                "close"
                * sd
            -1

# This unit test is supposed to check the functionality of the corresponding object.
[] > tests-returns-valid-posix-inet-addr-for-localhost
  code. > addr
    posix
      "inet_addr"
      * "127.0.0.1"
  or. > @
    os.is-windows
    addr.eq 16777343
