# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+package org.eolang
+alias org.eolang.io.stdout
+alias org.eolang.txt.sprintf
+junit

[] > converts-to-string
  eq. > @
    "42"
    42.as-string

[] > less-true
  less. > @
    10
    50

[] > less-equal
  not. > @
    less.
      10
      10

[] > less-false
  not. > @
    less.
      10
      -5

[] > greater-true
  greater. > @
    -200
    -1000

[] > greater-false
  not. > @
    greater.
      0
      100

[] > greater-equal
  not. > @
    greater.
      0
      0

[] > leq-true
  leq. > @
    -200
    -100

[] > leq-equal
  leq. > @
    50
    50

[] > leq-false
  not. > @
    leq.
      0
      -10

[] > geq-true
  geq. > @
    -1000
    -1100

[] > geq-equal
  geq. > @
    113
    113

[] > geq-false
  not. > @
    geq.
      0
      10

[] > eq-true
  eq. > @
    123
    123

[] > eq-false
  not. > @
    eq.
      123
      42

[] > compares-two-different-types
  not. > @
    eq.
      42
      "Hello!"

[] > neq-true
  neq. > @
    123
    125

[] > neq-false
  not. > @
    neq.
      -133
      -133

[] > calculates-fibonacci-number-with-recursion
  eq. > @
    3
    fibo 4
  [n] > fibo
    if. > @
      n.less 3
      1
      add.
        fibo (n.sub 1)
        fibo (n.sub 2)

[] > calculates-fibonacci-number-with-tail
  eq. > @
    3
    fibonacci 4
  [n] > fibonacci
    if. > @
      n.less 3
      small n
      rec n 1 1
    [n] > small
      if. > @
        n.eq 2
        1
        n
    [n minus1 minus2] > rec
      if. > @
        n.eq 3
        minus1.add minus2
        rec (n.sub 1) (minus1.add minus2) minus1

# Checks that division by zero returns an error object
[] > zero-division
  eq. > @
    (2.div 0).msg
    "Division by zero is undefined"

# Checks mathematical equality
# A = ((A div B) * B) + (A mod B)
[] > div-mod-compatibility
  -13 > dividend
  5 > divisor
  dividend.mod divisor > remainder
  dividend.div divisor > quotient
  eq. > @
    dividend
    add.
      remainder
      mul.
        divisor
        quotient

# Checks that division by one returns the dividend
[] > division-by-one
  -235 > dividend
  eq. > @
    dividend.div 1
    dividend

# Checks that div works properly with dividends greater than zero
[] > div-for-dividend-greater-than-zero
  256.div 16 > quotient
  quotient.eq 16 > @

# Checks div with remainder
[] > div-with-remainder
  (13.div -5).eq -2 > @

# Checks that int.as-float works properly
[] > as-float-works
  eq. > @
    -17.0
    17.neg.as-float

[] > abs-positive
  3.abs.eq 3 > @

[] > abs-negative
  -3.abs.eq 3 > @

[] > abs-zero
  0.abs.eq 0 > @

[] > to-bytes-and-backwards
  eq. > @
    42
    as-int.
      as-bytes.
        42

[] > as-bytes-equals-to-int
  eq. > @
    42
    42.as-bytes

[] > as-bytes-equals-to-int-backwards
  eq. > @
    42.as-bytes
    42

[] > xor-3-6
  eq. > @
    3.xor 6
    5

[] > left-1-2
  eq. > @
    1.left 2
    4

[] > right-8-2
  eq. > @
    8.right 2
    2

[] > as-hash-equals-to-similar-values
  eq. > @
    42.as-hash
    42.as-hash

# We can have collisions, so in this test just check if values not similar
[] > as-hash-have-different-hashes
  seq > @
    stdout
      sprintf
        "%d %d %d\n"
        41.as-hash
        42.as-hash
        43.as-hash
    TRUE

[] > or-1-2
  eq. > @
    1.or 2
    3

[] > and-3-6
  eq. > @
    3.and 6
    2
