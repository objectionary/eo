# The MIT License (MIT)
#
# Copyright (c) 2016-2021 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+package org.eolang
+junit

[] > matches-against-regular-expression
  (/([a-z])+/.matches "dude") > @

[] > calculates-length-of-spaces-only
  eq. > @
    " ".length
    1

[] > joins-array
  eq. > @
    "..".joined
      * "foo" "друг" "bar"
    "foo..друг..bar"

[] > trims-string
  eq. > @
    "Hello, dude"
    "Hello, dude  ".trim

[] > turns-string-into-int
  eq. > @
    42
    "42".as-int

[] > turns-string-into-float
  eq. > @
    3.1415926
    "3.1415926".as-float

[] > turns-string-into-bytes
  eq. > @
    D0-B4-D1-80-D1-83-D0-B3
    "друг".as-bytes

[] > bytes-equal-to-string
  eq. > @
    "друг"
    D0-B4-D1-80-D1-83-D0-B3

[] > string-equals-to-bytes
  eq. > @
    D0-B4-D1-80-D1-83-D0-B3
    "друг"

[] > picks-a-single-char
  eq. > @
    'e'
    "Hello, world!".char-at 1

[] > reads-the-length
  eq. > @
    12
    "Hello, друг!".length

[] > compares-two-different-types
  not. > @
    eq.
      "Hello"
      42

[] > text-block-one-line
  eq. > @
    "Abc"
    """
    Abc
    """

[] > text-block-tree-lines
  eq. > @
    65-0A-65-0A-65
    """
    e
    e
    e
    """.as-bytes

[] > text-block-with-margin
  eq. > @
    20-7A-0A-20-20-79-0A-20-78
    """
     z
      y
     x
    """.as-bytes

[] > compares-two-different-strings
  not. > @
    eq.
      "Hello"
      "Good bye"

[] > supports-escape-sequences
  eq. > @
    "Hello, друг!\n"
    "Hello, \u0434\u0440\u0443\u0433!\n"

[] > supports-escape-sequences-in-text
  eq. > @
    """
    Hello, \u0434\u0440\u0443\u0433!\n
    """
    "Hello, друг!\n"

[] > preserves-indentation-in-text
  eq. > @
    """
    a
     b
      c
    """
    "a\n b\n  c"
