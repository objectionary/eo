# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.hamcrest.assert-that
+home https://github.com/objectionary/eo
+junit
+package org.eolang
+version 0.0.0

[] > calculates-length-of-spaces-only
  assert-that > @
    " ".length
    $.equal-to 1

[] > turns-string-into-bytes
  assert-that > @
    "€ друг".as-bytes
    $.equal-to E2-82-AC-20-D0-B4-D1-80-D1-83-D0-B3

[] > bytes-equal-to-string
  assert-that > @
    D0-B4-D1-80-D1-83-D0-B3
    $.equal-to "друг"

[] > string-equals-to-bytes
  assert-that > @
    "друг"
    $.equal-to D0-B4-D1-80-D1-83-D0-B3

[] > reads-the-length
  assert-that > @
    "Hello, друг!".length
    $.equal-to 12

[] > compares-two-different-types
  assert-that > @
    "Hello"
    $.not
      $.equal-to 42

[] > text-block-one-line
  assert-that > @
    """
    Abc
    """
    $.equal-to "Abc"

[] > text-block-tree-lines
  assert-that > @
    """
    e
    e
    e
    """.as-bytes
    $.equal-to 65-0A-65-0A-65

[] > text-block-with-margin
  assert-that > @
    """
     z
      y
     x
    """.as-bytes
    $.equal-to 20-7A-0A-20-20-79-0A-20-78

[] > compares-two-different-strings
  assert-that > @
    "Hello"
    $.not
      $.equal-to "Good bye"

[] > supports-escape-sequences
  assert-that > @
    "Hello, \u0434\u0440\u0443\u0433!\n"
    $.equal-to "Hello, друг!\n"

[] > supports-escape-sequences-in-text
  assert-that > @
    """
    Hello, \u0434\u0440\u0443\u0433!\n
    """
    $.equal-to "Hello, друг!\n"

[] > preserves-indentation-in-text
  assert-that > @
    """
    a
     b
      c
    """
    $.equal-to "a\n b\n  c"

[] > similar-strings-generate-similar-hashes
  assert-that > @
    "hello".as-hash
    $.equal-to
      "hello".as-hash

[] > large-similar-strings-generate-similar-hashes
  assert-that > @
    "hello12".as-hash
    $.equal-to
      "hello12".as-hash

[] > compares-two-strings
  assert-that > @
    eq.
      "x"
      "x"
    $.equal-to TRUE

[] > one-symbol-string-compares
  assert-that > @
    "Ф"
    $.equal-to "Ф"

[] > supports-escape-sequences-line-break
  assert-that > @
    "\n"
    $.equal-to "\012"

[] > supports-escape-sequences-unicode
  assert-that > @
    "\u0424"
    $.equal-to "Ф"

[] > slice-from-start
  assert-that > @
    "hello".slice 0 1
    $.equal-to "h"

[] > slice-in-the-middle
  assert-that > @
    "hello".slice 2 3
    $.equal-to "llo"

[] > slice-from-the-end
  assert-that > @
    "hello".slice 4 1
    $.equal-to "o"

[] > slice-empty-string
  assert-that > @
    "".slice 0 0
    $.equal-to ""

[] > no-slice-string
  assert-that > @
    "no slice".slice 0 0
    $.equal-to ""

[] > slice-escape-sequences-line-break
  assert-that > @
    "\n".slice
      0
      "\n".length
    $.equal-to "\012"

[] > slice-escape-sequences-unicode
  assert-that > @
    "\u0424".slice
      0
      "\u0424".length
    $.equal-to "Ф"

[] > slice-foreign-literals
  assert-that > @
    "привет".slice
      0
      1
    $.equal-to "п"

[] > slice-foreign-literals-2
  assert-that > @
    "hello, 大家!".slice
      7
      1
    $.equal-to "大"

[] > slice-start-below-zero
  assert-that > @
    try
      []
        slice. > @
          "some string"
          -1
          1
      [e]
        e > @
      nop
    $.equal-to
      "Start index must be greater than 0 but was -1"

[] > slice-end-below-start
  assert-that > @
    try
      []
        slice. > @
          "some string"
          2
          -1
      [e]
        e > @
      nop
    $.equal-to
      "End index must be greater or equal to start but was 1 < 2"

[] > slice-end-greater-actual
  assert-that > @
    try
      []
        slice. > @
          "some string"
          7
          5
      [e]
        e > @
      nop
    $.equal-to
      "Start index + length must not exceed string length but was 12 > 11"
