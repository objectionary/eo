# The MIT License (MIT)
#
#  2016-2022 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.0.0
+version 0.0.0

# Allow to choose right options according to cases conditions.
# Parameter cases is the array of two dimensional array, which
# consist of condition bool value and expected value, if this
# condition is TRUE.
# e.g.
# switch
#   *
#     TRUE
#     "this value will be returned"
#   *
#     FALSE
#     "this value will be skipped"
#
# This object returns value of only first truly statement.
[cases] > switch
  cases.length > len!
  # Take case by given index.
  # If case key is TRUE, return case value.
  # Otherwise take next case.
  [index] > case-at
    if. > @
      index.eq ^.len
      TRUE
      if.
        at.
          ^.cases.at index > case!
          0
        case.at 1
        ^.case-at
          index.plus 1
  if. > @
    cases.length.eq 0
    error "switch cases are empty"
    case-at 0
