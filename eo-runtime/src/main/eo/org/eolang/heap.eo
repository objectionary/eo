# The MIT License (MIT)
#
#  2016-2022 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.0.0
+version 0.0.0

# Emulation of random-access memory (RAM).
[size] > heap
  # Allocates a block in memory of "s" bytes,
  # and returns a pointer to it.
  # The "length" attribute of the pointer won't be bound.
  [s] > malloc
    memory 0 > next
    plus. > new-next!
      s
      ^.malloc.next.as-int
    if. > @
      gt.
        new-next
        size
      error
        "Allocation failed: bad alloc (not enough memory in the heap)"
      seq
        *
          write.
            ^.malloc.next
            new-next
          0.plus
            ^.malloc.next.as-int

  # Freed a block in memory, which is referred by the
  # given pointer, which was earlier obtained with malloc().
  # Returns TRUE if works. If the pointer provided wasn't
  # previously allocated, the result will be [].
  [p] > free
    seq > @
      *
        p
        TRUE

  # Pointer inside the heap.
  [address length] > pointer
    address > @

    # Increment this pointer by x steps forward.
    [x] > add
      &.^.pointer > @
        address.plus
          length.times x
        length

    # Decrement this pointer by x steps backward.
    [x] > sub
      &.add > @
        x.times -1

    # One block in the heap.
    [len inverse] > block /?
