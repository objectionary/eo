+alias org.eolang.structs.list
+alias org.eolang.txt.sprintf
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.structs
+version 0.0.0
+spdx SPDX-FileCopyrightText Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:19
+unlint redundant-object:25
+unlint redundant-object:393

# List implements based operations on collections like reducing, mapping, filtering, etc.
# Decorates and extends `tuple`.
[origin] > list
  origin > @
  # Is it empty?.
  # A check to determine if an object contains no elements or data.
  0.eq origin.length > is-empty
  # Returns a new list sorted via `.lt` method.
  # @todo #3251:30min The object does not work. After moving `list` object
  #  to eo-runtime this is the only method which does not work because it's quite
  #  hard to implement properly, especially after big changes in EO semantic.
  #  We need to get it done and write some tests for it.
  $ > sorted

  # Create a new list with this element added to the end of it.
  [x] > with
    list > @
      origin.with x

  # Create a new list with an element inserted by the provided index.
  [index item] > withi
    concat. > @
      with.
        head index
        item
      tail
        origin.length.minus index

  # Reduce with index from "start" using the function "func".
  # Here "func" must be an abstract object with three free attributes.
  # The first one for the accumulator, the second one
  # for the element of the collection and the third one for the index.
  [start func] > reducedi
    if. > @
      is-empty
      start
      rec-reducedi origin

    [tup] > rec-reducedi
      if. > @
        tup.length.eq 1
        func
          start
          tup.value
          0
        func
          rec-reducedi tup.prev
          tup.value
          tup.prev.length

  # Reduce from "start" using the function "func".
  # Here "func" must be an abstract object with two free attributes.
  # The first one for the accumulator, the second one for the element
  # of the collection.
  [start func] > reduced
    reducedi > @
      start
      func accum item > [accum item idx] >>

  # Map with index. Here "func" must be an abstract
  # object with two free attributes. The first
  # one for the element of the collection, the second one
  # for the index.
  [func] > mappedi
    list > @
      reducedi
        *
        [accum item idx] >>
          accum.with > @
            func item idx

  # Map without index. Here "func" must be an abstract
  # object with one free attribute, for the element
  # of the collection.
  [func] > mapped
    mappedi > @
      func item > [item idx] >>

  # For each collection element dataize the object
  # Here "func" must be an abstract object with
  # two free attributes: the element of the
  # collection and its index.
  # The result of `func` must be dataizable.
  [func] > eachi
    reducedi > @
      true
      [acc item index] >>
        seq * > @
          acc
          func item index

  # For each collection element dataize the object
  # Here "func" must be an abstract object with
  # one free attribute, the element of the collection.
  [func] > each
    eachi > @
      func item > [item index] >>

  # Create a new list without the i-th element.
  [i] > withouti
    list > @
      reducedi
        *
        [accum item idx] >>
          if. > @
            i.eq idx
            accum
            accum.with item

  # Create a new list without the `element` which is `.eq` to given one.
  [element] > without
    list > @
      reduced
        *
        [accum item] >>
          if. > @
            element.eq item
            accum
            accum.with item

  # Checks if all elements of the current list are `.eq`
  # to the all elements of given collection.
  [other] > eq
    and. > @
      eq.
        origin.length
        other.length
      rec-eq origin other

    [first second] > rec-eq
      if. > @
        first.length.eq 0
        true
        and.
          first.value.eq second.value
          rec-eq first.prev second.prev

  # Concatenates current list with given one.
  [passed] > concat
    (list passed).reducedi > @
      ^
      accum.with item > [accum item index]

  # Returns index of the first particular item in list.
  # If the list has no this item, index-of returns -1.
  [wanted] > index-of
    number > @
      rec-index-of origin

    [tup] > rec-index-of
      if. > @
        tup.length.eq 0
        -1
        if.
          next.eq -1
          if.
            tup.value.eq wanted
            tup.prev.length
            -1
          next
      rec-index-of tup.prev > next!

  # Returns index of the last particular item in list.
  # If the list has no this item, returns -1.
  [wanted] > last-index-of
    rec-last-index-of origin > @

    [tup] > rec-last-index-of
      if. > @
        tup.length.eq 0
        -1
        if.
          tup.value.eq wanted
          tup.prev.length
          rec-last-index-of tup.prev

  # Returns `true` if the list contains `element`.
  # Otherwise, `false`.
  [element] > contains
    not. > @
      eq.
        -1
        index-of element

  # Filter list with index with the function `func`.
  # Here `func` must be an abstract
  # object with two attributes. The first
  # one for the element, the second one
  # for the index. The result of dataization
  # the `func` should be boolean, that is `true` or `false`.
  [func] > filteredi
    list > @
      rec-filteredi origin

    [tup] > rec-filteredi
      if. > @
        tup.length.eq 0
        *
        if.
          func tup.value tup.prev.length
          next.with tup.value
          next
      rec-filteredi tup.prev > next

  # Filter list without index with the function `func`.
  # Here `func` must be an abstract object
  # with one attribute for the element.
  # The result of dataization the `func`
  # should be boolean, that is `true` or `false`.
  [func] > filtered
    filteredi > @
      func item > [item index] >>

  # Get the first `index` elements from the start of the list.
  [index] > head
    if. > @
      idx.eq 0
      list *
      if.
        0.gt idx
        tail (number idx).neg
        if.
          (number idx).gte origin.length
          ^
          list
            rec-head origin
    index > idx!

    [tup] > rec-head
      if. > @
        tup.length.eq idx
        tup
        rec-head tup.prev

  # Get the last `index` elements from the end of the list.
  [index] > tail
    if. > @
      0.gt start
      ^
      list
        reducedi
          *
          [accum item idx] >>
            if. > @
              idx.gte start
              accum.with item
              accum
    index > idx!
    origin.length.minus idx > start!

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-should-not-be-empty
    not. > @
      is-empty.
        list
          * 1 2

  # This unit test is supposed to check the functionality of the corresponding object.
  (list *).is-empty > [] +> list-should-be-empty

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-should-not-be-empty-with-three-objects
    (list xs).is-empty.not > @
    * > xs
      [x]
      [y]
      [z]

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-should-not-be-empty-with-one-anon-object
    (list xs).is-empty.not > @
    * > xs
      [f]

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-simple-with
    eq. > @
      with.
        list
          * 1 2
        3
      * 1 2 3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-simple-insert
    eq. > @
      withi.
        list
          * 1 2 3 4 5
        3
        "hello"
      * 1 2 3 "hello" 4 5

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-insert-with-zero-index
    eq. > @
      withi.
        list
          * 1 2 3 4 5
        0
        "hello"
      * "hello" 1 2 3 4 5

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-reduce-list-with-index
    eq. > @
      6
      reducedi.
        list
          * 1 1
        0
        [a x i] >>
          plus. > @
            x
            a.plus
              (* 2 2).at i

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-reducedi-long-int-array
    eq. > @
      reducedi.
        list
          * 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1
        0
        x.plus a > [a x i]
      1

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-reducedi-bools-array
    not. > @
      reducedi.
        list
          * true true false
        true
        x.and a > [a x i]

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-reducedi-nested-functions
    eq. > @
      reducedi.
        list
          * 10 500
        0
        [acc x i]
          check x > @
          [el] > check
            if. > @
              ^.x.lt 100
              ^.acc.plus x
              ^.acc.minus x
      plus.
        10
        0.minus 500

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-reduce-without-index
    eq. > @
      reduced.
        list
          * 1 2 3 4
        -1
        a.times x > [a x]
      -24

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-reduced-printing
    eq. > @
      reduced.
        list
          * 0 1
        ""
        [acc item]
          acc.concat > @
            sprintf
              "%d"
              * item
      "01"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-complex-objects-list-comparison
    list > res
      *
        list (* 0 1)
        list (* 4 0)
        list (* 7 3)
    .eq > @
      *
        * 0 1
        * 4 0
        * 7 3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-mappedi-should-work
    eq. > @
      mappedi.
        list
          * 1 2 3 4
        i.times x > [x i]
      * 0 2 6 12

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-simple-list-mapping
    eq. > @
      mapped.
        list
          * 1 2 3
        x.times 2 > [x]
      * 2 4 6

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-iterates-with-eachi
    eq. > @
      malloc.for
        0
        [m]
          list
            * 1 2 3
          .eachi > @
            [item index] >>
              ^.m.put > @
                plus.
                  ^.m.as-number.plus item
                  index
      9

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-iterates-with-each
    eq. > @
      malloc.for
        0
        [m]
          list
            * 1 2 3
          .each > @
            ^.m.put (^.m.as-number.plus i) > [i] >>
      6

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-withouti
    eq. > @
      withouti.
        list
          * 1 2 3
        1
      * 1 3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-withouti-complex-case
    eq. > @
      foo
        * 1 "text" "f"
      * "text" "f"
    [a] > foo
      withouti. > @
        list a
        0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-withouti-nested-array
    eq. > @
      withouti.
        list
          * "smthg" 27 (* 3 2 1)
        2
      * "smthg" 27

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-without
    eq. > @
      without.
        list
          * 1 2 1 2 1 5
        2
      * 1 1 1 5

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-equality-test
    eq. > @
      list
        * 1 2 3
      * 1 2 3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-not-equality-test
    not. > @
      eq.
        list
          * 1 2 3
        * 3 2 1

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-concatenates-lists
    and. > @
      eq.
        1
        list3.at 0
      eq.
        list3
        * 1 2 3
    withouti. > list3
      concat.
        list (* 0 1)
        list (* 2 3)
      0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-concatenates-with-tuple
    eq. > @
      concat.
        list
          * 0 1
        * 2 4
      * 0 1 2 4

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-returns-index-of
    eq. > @
      1
      index-of.
        list
          * 1 2 3
        2

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-returns-first-index-of-element
    eq. > @
      0
      index-of.
        list
          * -1 2 -1
        -1

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-does-not-find-index-of
    eq. > @
      -1
      index-of.
        list
          * "qwerty" 2 3
        7

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-finds-index-of-string
    eq. > @
      1
      index-of.
        list
          * "asdfgh" "qwerty" 3
        "qwerty"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-finds-last-index-of
    eq. > @
      1
      last-index-of.
        list
          * "qwerty" 2 3
        2

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-finds-last-index-of-repeated
    eq. > @
      2
      last-index-of.
        list
          * 24 42 24
        24

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-last-index-of-not-found
    eq. > @
      -1
      last-index-of.
        list
          * 1 2 3
        0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-last-index-of-empty
    eq. > @
      -1
      last-index-of.
        list *
        "abc"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-last-index-of-unicode
    eq. > @
      3
      last-index-of.
        list
          * "Hi" "Привет" "Hola" "Привет" "こんにちわ"
        "Привет"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-contains-string
    contains. > @
      list
        * "qwerty" "asdfgh" 3 "qwerty"
      "qwerty"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-contains-number
    contains. > @
      list
        * "qwerty" "asdfgh" 3 "qwerty"
      3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-does-not-contain
    not. > @
      contains.
        list
          * "Привет" "asdfgh" 3 "qwerty"
        "Hi"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-filteredi-with-lt
    eq. > @
      filteredi.
        list
          * 3 1 4 2 5
        v.lt 3 > [v i]
      * 1 2

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-filteredi-with-string-length
    eq. > @
      filteredi.
        list
          * "Hello" "Name" "EO" "List"
        v.length.gt 4 > [v i]
      * "Hello"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-filteredi-with-empty-list
    is-empty. > @
      filteredi.
        list
          *
        v.lt 3 > [v i]

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-filteredi-by-index
    eq. > @
      filteredi.
        list
          * 3 1 4
        i.gt 0 > [v i]
      * 1 4

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-filteredi-with-bools
    eq. > @
      filteredi.
        list
          * true false true
        v.not > [v i]
      * false

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-simple-filtered
    eq. > @
      filtered.
        list
          * 3 1 4 2 5
        v.gt 2 > [v]
      * 3 4 5

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-filtered-with-bools
    eq. > @
      filtered.
        list
          * true false true
        v.not > [v]
      * false

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-simple-head
    eq. > @
      head.
        list
          * 1 2 3 4 5
        1
      * 1

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-head-with-zero-index
    is-empty. > @
      head.
        list
          * 1 2 3
        0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-list-head-with-length-index
    eq. > @
      head.
        list
          * 1 2 3
        3
      * 1 2 3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-complex-head
    eq. > @
      head.
        list
          * "foo" 2.2 00-01 "bar"
        2
      * "foo" 2.2

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-head-with-negative
    eq. > @
      head.
        list
          * 1 2 3
        -1
      * 3

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-complex-head-with-negative
    eq. > @
      head.
        list
          * "foo" 2.2 00-01 "bar"
        -3
      * 2.2 00-01 "bar"

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-simple-tail
    eq. > @
      tail.
        list
          * 1 2 3 4 5
        2
      * 4 5

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-zero-index-in-tail
    is-empty. > @
      tail.
        list
          * 1 2 3 4 5
        0

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-large-index-in-tail
    eq. > @
      tail.
        list
          * 1 2 3 4 5
        10
      * 1 2 3 4 5
