+alias org.eolang.tt.text
+alias org.eolang.ss.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Returns concatenation of all `other` strings.
# Here `others` must be a `tuple` of `strings`.
[txt others] > chained
  if. > @
    0.eq others.length
    txt
    text
      string
        reduced.
          list others
          txt.as-bytes
          accum.concat str.as-bytes > [accum str]

  # Tests specific functionality chained method concatenates text with multiple other strings.
  [] +> tests-chains-with-other-strings
    eq. > @
      "Hello, world!"
      chained
        text "Hello"
        *
          ", "
          "world"
          "!"

  # Tests specific functionality chained method returns original text when no strings provided.
  [] +> tests-returns-same-text-on-chaining-with-no-strings
    eq. > @
      "Some"
      chained
        text "Some"
        *