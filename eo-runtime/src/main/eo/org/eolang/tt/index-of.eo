+alias org.eolang.tt.text
+alias org.eolang.tt.slice
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Returns index of `substring` in current `text`.
# If no `substring` was found, it returns -1.
[txt substring] > index-of
  if. > @
    or.
      or.
        (number substring-size).gt txt-size
        and.
          substring-size.eq txt-size
          (substring-bts.eq txt-bts).not
      found.eq -1
    -1
    length.
      string
        slice txt-bts 0 found
  txt > txt-bts!
  substring > substring-bts!
  txt-bts.size > txt-size!
  substring-bts.size > substring-size!
  (number txt-size).minus substring-size > end!
  rec-index-of 0 > found!

  [idx] > rec-index-of
    if. > @
      end.eq idx
      if.
        includes
        idx
        -1
      if.
        includes
        idx
        rec-index-of (idx.plus 1).as-number
    eq. > includes
      slice txt-bts idx substring-size
      substring-bts

  # Tests specific functionality index-of returns -1 for non-existent substring longer than text.
  [] +> tests-index-of-non-existed-substring-with-more-length
    eq. > @
      -1
      index-of (text "Hello") "Somebody"

  # Tests specific functionality index-of returns -1 for non-existent substring of same length.
  [] +> tests-index-of-non-existed-substring-with-same-length
    eq. > @
      -1
      index-of (text "Hello") "world"

  # Tests specific functionality index-of returns -1 for non-existent substring in UTF text.
  [] +> tests-index-of-non-existed-substring-with-utf
    eq. > @
      -1
      index-of
        text "привет, друг!"
        "x"

  # Tests specific functionality index-of finds correct position of UTF character in text.
  [] +> tests-index-of-existed-substring-with-utf
    eq. > @
      3
      index-of
        text "привет, друг!"
        "в"

  # Tests specific functionality index-of returns correct position of newline character.
  [] +> tests-returns-valid-index-of-substring
    eq. > @
      6
      index-of
        text "Hello \n world"
        "\n"

  # Tests specific functionality index-of finds substring located at end of text.
  [] +> tests-returns-valid-index-of-substring-in-the-end
    eq. > @
      6
      index-of (text "Hello world") "world"