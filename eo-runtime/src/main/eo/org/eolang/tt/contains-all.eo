+alias org.eolang.tt.text
+alias org.eolang.tt.contains
+alias org.eolang.ss.list
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Checks if current `text` contains all elements from `substrings`
[txt substrings] > contains-all
  reduced. > @
    mapped.
      substrings
      contains (text txt-bts) x > [x] >>
    true
    x.and a > [a x]
  txt > txt-bts!

  # Tests successful contains-all
  [] +> tests-successful-contains-all
    contains-all (text "abcdefghijk") (list (* "abc" "ghi")) > @

  # Tests not contains-all
  [] +> tests-not-contains-all
    not. > @
      contains-all (text "qwerty") (list (* "qer" "ty" "abc"))

  # Tests contains-all vertical
  [] +> tests-contains-all-vert
    contains-all > @
      text "Hello, world!"
      list (* "ell" " " "," "d!")

  # Tests contains-all without substrings
  [] +> test-contains-all-without-substrings
    contains-all (text "xyz") (list *) > @

  # Tests contains-all for empty text and without substrings
  [] +> tests-contains-all-for-empty-text-and-without-substrings
    contains-all (text "") (list *) > @