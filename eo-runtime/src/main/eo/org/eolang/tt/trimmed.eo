+alias org.eolang.tt.text
+alias org.eolang.tt.trimmed-left
+alias org.eolang.tt.trimmed-right
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Returns `text` trimmed from both sides.
[str] > trimmed
  if. > @
    0.eq str.as-bytes.size
    text str
    trimmed-left
      trimmed-right
        str

  # Tests specific functionality trimmed removes single leading space from text.
  [] +> tests-text-trimmed-one-space-left
    eq. > @
      trimmed (text " some")
      "some"

  # Tests specific functionality trimmed removes single trailing space from text.
  [] +> tests-text-trimmed-one-space-right
    eq. > @
      trimmed (text "some ")
      "some"

  # Tests specific functionality trimmed removes spaces from both sides of text.
  [] +> tests-text-trimmed-one-space-both
    eq. > @
      trimmed (text " some ")
      "some"

  # Tests specific functionality trimmed removes multiple spaces from both sides of text.
  [] +> tests-text-trimmed-many-spaces
    eq. > @
      trimmed (text "    some     ")
      "some"

  # Tests specific functionality trimmed returns empty string when applied to empty text.
  [] +> tests-text-trimmed-empty
    eq. > @
      trimmed (text "")
      ""

  # Tests specific functionality trimmed returns empty string when text contains only spaces.
  [] +> tests-text-trimmed-only-spaces
    eq. > @
      trimmed (text "        ")
      ""