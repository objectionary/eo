+alias org.eolang.tt.text
+alias org.eolang.tt.slice
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.tt
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT

# Checks that current `text` ends with given `substring`.
[txt substring] > ends-with
  and. > @
    (number substring-size).lte txt-size
    eq.
      slice
        txt-bts
        (number txt-size).minus substring-size
        substring-size
      substring-bts
  txt > txt-bts!
  substring > substring-bts!
  txt-bts.size > txt-size!
  substring-bts.size > substring-size!

  # Tests specific functionality ends-with returns true when text ends with given substring.
  [] +> tests-ends-with-substring
    ends-with (text "Hello world!") "world!" > @

  # Tests specific functionality ends-with returns false when text does not end with substring.
  [] +> tests-does-not-end-with-substring
    not. > @
      ends-with (text "Hello world!") "Hello"