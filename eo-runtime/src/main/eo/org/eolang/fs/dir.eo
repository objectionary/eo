+alias org.eolang.txt.sprintf
+alias org.eolang.fs.tmpdir
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.fs
+rt jvm org.eolang:eo-runtime:0.0.0
+rt node eo2js-runtime:0.0.0
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:18
+unlint redundant-object:19

# Directory in the file system.
# Apparently every directory is a file.
[file] > dir
  $.file > @
  $ > as-dir
  true > is-directory

  # Makes a directory together with all required
  # parent directories and returns the created directory.
  [] > made
    as-dir. > @
      if.
        ^.exists
        ^
        seq *
          mkdir
          ^

    # Makes a directory together with all required
    # parent directories.
    # Returns `org.eolang.true` object.
    #
    # Note: This object is intended for internal use only. Please
    # don't use it programmatically outside of `dir` object.
    [] > mkdir ?

  # Goes through all files in the directory, recursively
  # finding them with the `glob` provided.
  # Returns `org.eolang.tuple` of all files in the directory.
  [glob] > walk ?

  # Deletes directory and all files in it, recursively.
  # Returns the deleted directory.
  [] > deleted
    as-dir. > @
      if.
        ^.exists
        seq *
          rec-delete (walk "**").@
          ^
        ^

    # Deletes files and directories in the current directory recursively.
    # Returns `true`.
    #
    # Note: This object is intended for internal use only. Please
    # don't use the object programmatically outside of `dir` object.
    [tup] > rec-delete
      if. > @
        tup.length.eq 0
        true
        seq *
          tup.head.deleted
          rec-delete tup.tail

  # Creates an empty temporary file in the current directory.
  [] > tmpfile
    as-file. > @
      if.
        ^.exists
        QQ.fs.file
          touch.as-bytes
        error
          sprintf
            "Directory %s does not exist, can't create temporary file"
            * file

    # Creates an empty temporary file in the current directory and
    # returns absolute path to it as `org.eolang.string`.
    #
    # Note: This object is intended for internal use only. Please
    # don't use the object programmatically outside of the `dir` object.
    [] > touch ?

  # Opens the file for I/O operations.
  # Since current file is a directory - returns an `error`.
  [mode scope] > open
    error > @
      sprintf
        "The file %s is a directory, can't open for I/O operations"
        * file

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-makes-new-directory
    and. > @
      d.exists
      d.is-directory
    (tmpdir.tmpfile.deleted.as-path.resolved "foo-new").as-dir.made > d

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-deletes-empty-directory
    tmpdir
    .tmpfile
    .deleted
    .as-path
    .resolved
      "bar-empty"
    .as-dir
    .made
    .deleted
    .exists
    .not > @

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> throws-on-opening-directory
    tmpdir.open > @
      "w"
      true > [d]

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-deletes-directory-with-files-recursively
    and. > @
      and.
        and.
          and.
            first.exists
            second.exists
          d.deleted.exists.not
        first.exists.not
      second.exists.not
    (dir tmpdir.tmpfile.deleted).made > d
    d.tmpfile > first
    d.tmpfile > second

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-deletes-directory-with-file-and-dir
    and. > @
      and.
        and.
          and.
            and.
              inner.is-directory
              inner.exists
            f.exists
          d.deleted.exists.not
        inner.exists.not
      f.exists.not
    (dir tmpdir.tmpfile.deleted).made > d
    (dir d.tmpfile.deleted).made > inner
    d.tmpfile > f

  # This unit test is supposed to check the functionality of the corresponding object.
  [] +> tests-walks-recursively
    seq > @
      *
        (d.resolved "foo/bar").as-dir.made
        (d.resolved "foo/bar/test.txt").as-file.touched
        (d.resolved "x/y/z").as-dir.made
        (d.resolved "x/y/z/a.txt").as-file.touched
        (d.as-dir.walk "**/*.txt").length.eq 2
    (dir tmpdir.tmpfile.deleted).made.as-path > d
