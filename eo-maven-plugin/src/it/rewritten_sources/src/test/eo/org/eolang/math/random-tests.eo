# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.hamcrest.assert-that
+alias org.eolang.math.random
+home https://github.com/objectionary/eo-math
+package org.eolang.math
+tests
+version 0.0.0

[] > random-with-seed
  random 51 > r
  assert-that > @
    r.next
    $.not
      $.equal-to (r.next.next)

[] > seeded-randoms-are-equal
  assert-that > @
    (random 1654).next.next.next
    $.equal-to
      (random 1654).next.next.next

[] > random-is-in-range
  random 123 > r
  assert-that > @
    and.
      r.lt 1.0
      not.
        r.lt 0.0
      r.next.lt 1.0
      not.
        r.next.lt 0.0
      r.next.next.lt 1.0
      not.
        r.next.next.lt 0.0
    $.is
      $.equal-to TRUE

[] > two-random-numbers-with-const
  random.pseudo > r!
  nop > @
    assert-that
      r
      $.equal-to r

[] > two-random-numbers-with-phi
  (random 123).@ > r
  assert-that > @
    r
    $.equal-to r

[] > two-random-numbers-with-increment-with-const
  random.pseudo.plus 3.1416 > r!
  nop > @
    assert-that
      r
      $.equal-to r

[] > two-random-numbers-with-increment
  random.pseudo.plus 3.1416 > r
  nop > @
    if.
      QQ.sys.uname.is-windows
      nop
      assert-that
        r
        $.not
          $.equal-to r
