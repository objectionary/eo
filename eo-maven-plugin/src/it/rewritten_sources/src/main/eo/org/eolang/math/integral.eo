# The MIT License (MIT)
#
# Copyright (c) 2021-2022 Max Trunnikov
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.math.number
+home https://github.com/objectionary/eo-math
+package org.eolang.math
+rt jvm org.eolang:eo-math:0.0.0
+version 0.0.0

# Counts integral from a to b
[fun a b] > integral
  [fun a b] > subsection
    times. > @
      div.
        minus.
          b
          a
        6.0
      plus.
        fun a
        times.
          4.0
          fun
            div.
              plus.
                a
                b
              2.0
        fun b
  memory > sum
    0.0
  memory > x
    (number a).as-float
  (number b).as-float > y!
  100.0 > n!
  div. > h!
    minus.
      y
      x
    n
  seq > @
    while.
      lt.
        plus.
          x
          h
        y
      [i]
        seq > @
          sum.write
            plus.
              sum
              subsection
                fun
                x
                plus.
                  x
                  h
          x.write
            plus.
              x
              h
    sum
